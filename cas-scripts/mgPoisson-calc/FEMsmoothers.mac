/* Kernels for the smoothing/relaxation operators for
   the Poisson equation
         - L(phi) = rho
   where L=Laplacian, using an FEM method. */

load("modal-basis");
load("out-scripts");
load("nodalOperations/nodalFunctions");
fpprec : 24$

/* String used for identifying nearest neighbor cells. */
neighStrKey : [[-1,"L"],[0,""],[1,"U"]]$

/* Types of boundary conditions for which to generate kernels.
     =0 periodic
     =1 Dirichlet
     =2 Neumann
     =3 Robin
   For periodic we'll assume ghost cells are filled accordingly
   and use an interior stencil. */
bcTypes  : [0, 1, 2, 3]$
bcStrs   : ["", "Dirichlet", "Neumann", "Robin"]$
bcKey    : makelist([bcTypes[i],bcStrs[i]],i,1,length(bcTypes))$


calcJacobiGaussSeidelOp(fh, funcNm, dim, basisFun, pMax, isJacobi) := block([],
  /* Cellwise (damped) Gauss-Seidel relaxation on the Poisson equation using an FEM method. 
     Beware that cellwise might not mean exactly the cell-wise used in the literature. Here
     we mean that we are solving the linear system that links the nodes stored in a single
     single cell, but those are not all the nodes abutting a cell.*/

  kill(varsC, varsP, basisC, basisP),

  load(sconcat("basis-precalc/basis", basisFun, dim, "x")),

  /* Associative list between stencil location (-1,0,1) and variable
     strings used in kernel names. */
  coordVarS : getCoordVarS(varsC),

  /* List of stencil locations. Each entry is a dim-dimensional list
     with -1 meaning it is at a lower boundary, 1 upper boundary, or
     0 away from boundaries in that direction. */
  stencilLoc : getStencilCoords(dim),

  for sI : 1 thru length(stencilLoc) do (

    /* Obtain the different BC combinations for this stencil.
       Will generate a separate kernel for each. */
    bcCombos : getBCcombos(stencilLoc[sI],bcTypes),

    /* Loop over BC combinations. Will create a separate kernel for each. */
    for bcI : 1 thru length(bcCombos) do (

      currBC : copylist(bcCombos[bcI]),

      /* String used in kernel name, identifying stencil location and BC. */
      stencilStr : getStencilStr(stencilLoc[sI],boundStrKey,coordVarS,currBC,bcKey),

      for polyOrder : 1 thru pMax do (

        /* Each cell only stores the lower left nodes. */
        numStoredNodes : length(getStoredNodes(args(getNodes(basisFun, dim, polyOrder)),stencilLoc[sI])),

        /* Obtain the weak Poisson equation for the stored nodes. */
        weqPoissonLst : weakPoissonStored(basisFun,varsC,polyOrder,stencilLoc[sI],currBC),
        poissonLHS    : weqPoissonLst[1],
        sourceRHS     : weqPoissonLst[2],

        /* Solve for the coefficients of this cell to get the stencil. */
        phiC_c  : makelist(phiC[i],i,0,numStoredNodes-1),
        weqSolv : linsolve(makelist(poissonLHS[i]=sourceRHS[i],i,1,numStoredNodes),phiC_c),

        if isJacobi then (
          /* (Cellwise) damped Jacobi method. */
          phiPrevC_c   : makelist(phiPrevC[i],i,0,numStoredNodes-1),
          relaxStencil : (1-omega)*phiPrevC_c + omega*makelist(rhs(weqSolv[i]),i,1,numStoredNodes)
        ) else (
          /* (Cellwise) damped Gauss-Seidel method. */
          relaxStencil : (1-omega)*phiC_c + omega*makelist(rhs(weqSolv[i]),i,1,numStoredNodes)
        ),
        relaxStencil : fullratsimp(relaxStencil),

        printf(fh, "void ~a_~aP~a(const double omega, double **dx, const double *bcVals, double **rho, double **phiPrev, double **phi) ~%{ ~%", funcNm, stencilStr, polyOrder),
        printf(fh, "  // omega:   relaxation parameter.~%"),
        printf(fh, "  // dx:      cell lengths of cells pointed to by the stencil.~%"),
        printf(fh, "  // bcVals:  values to impose as BCs.~%"),
        printf(fh, "  // rho:     right-side source in the current cell.~%"),
        printf(fh, "  // phiPrev: (Jacobi-only) iterate cells pointed to by the stencil (only use neighbor cells).~%"),
        printf(fh, "  // phi:     iterate cells pointed to by the stencil (Gauss-Seidel), or cell we are currently updating (Jacobi).~%"),
        printf(fh, "~%"),

        /* Pointers to the cell lengths in cells pointed to by the stencil. */
        printf(fh, "  double *dxC  = dx[0]; ~%"),
        printf(fh, "~%"),

        /* This volume factors appears in front of the right-side source term. */
        volumeFac : (1/2)^dim,
        for dir : 1 thru dim do (
          volumeFac : volumeFac*dxC[dir-1]
        ),
        printf(fh, "  double volFac = ~a; ~%", float(volumeFac)),
        printf(fh, "~%"),

        /* Some factors based on cell lengths. */
        printf(fh, "  double rdx2SqVol[~a]; ~%", dim),

        for dir : 1 thru dim do (
          printf(fh, "  rdx2SqVol[~a] = volFac*4.0/(dxC[~a]*dxC[~a]); ~%", dir-1, dir-1, dir-1)
        ),
        printf(fh, "~%"),

        /* Pointers to the data in cells pointed to by the stencil. */
        printf(fh, "  double *phiC = phi[0]; ~%"),
        if isJacobi then (
          printf(fh, "  double *phiPrevC = phiPrev[0]; ~%"),
          phiStr : "Prev"
        ) else (
          phiStr : ""
        ),
        printf(fh, "  double *rhoC = rho[0]; ~%"),
        i : 1,
        varStrs : [""],
        for d : 1 thru dim do (
          for sI : 1 thru 3^(d-1) do (
            for pm : -1 thru 1 step 2 do (
              i : i + 1,
              varStrs : append(varStrs, [sconcat(varStrs[sI],assoc(pm,neighStrKey),string(varsC[d]))]),
              printf(fh, "  double *phi~a = phi~a[~a]; ~%", varStrs[i], phiStr, i-1),
              printf(fh, "  double *rho~a = rho[~a]; ~%", varStrs[i], i-1)
            )
          )
        ),
        printf(fh, "~%"),

        /* Write the relaxation stencil. */
        writeCExprs1noPowers(phiC,relaxStencil,[rdx2SqVol,bcVals],12),
        printf(fh, "~%"),

        printf(fh, "}~%"),
        printf(fh, "~%")

      )
    )
  )
)$
