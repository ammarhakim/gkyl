load("modal-basis")$
load("out-scripts")$
load(stringproc)$
load("scifac")$
load("fem-calc/femMatrices")$
fpprec : 24$

wc : [wx, wy, wz, wv, wm]$
wc2 : [wx2, wy2, wz2, wv2, wm2]$
derivfac : [dfac_x, dfac_y, dfac_z, dfac_v, dfac_m]$
derivfac2 : [dfac_x2, dfac_y2, dfac_z2, dfac_v2, dfac_m2]$
gkvsub:[vx=vpar,vy=mu]$

let (wv^2, wv2)$
let (dfac_v^2, dfac_v2)$

doMakeExprLst(vals, S) := makelist(if vals[i] # 0 then S[i-1] else 0, i, 1, length(vals))$
doExpandLst(lst, basis) := sum(lst[i]*basis[i], i, 1, length(basis))$

/* set up some vector calculus functions */
load("vect")$
fgrad(f):= ev(express(grad(f)),diff)*derivfacsC$

gkPB(f,g):=1/m_*(Bstar_by_Bmag_expd.fgrad(f)*dfac_v*diff(g,vpar)-Bstar_by_Bmag_expd.fgrad(g)*dfac_v*diff(f,vpar))-1/q_*geoZ_expd*dfac_x*dfac_y*(diff(f,x)*diff(g,y)-diff(f,y)*diff(g,x)) + 1/q_*geoY_expd*dfac_x*dfac_z*(diff(f,x)*diff(g,z)-diff(f,z)*diff(g,x)) - 1/q_*geoX_expd*dfac_y*dfac_z*(diff(f,y)*diff(g,z)-diff(f,z)*diff(g,y))$

calcGKVolUpdater(fh, funcNm, cdim, vdim, basisFun, i, emflag, bvarslist) := block([],
kill(varsC, varsP, basisC, basisP),
if vdim > 0 then modNm : sconcat("basis-precalc/basis", basisFun, cdim, "x", vdim, "v")
else modNm : sconcat("basis-precalc/basis", basisFun, cdim, "x"),
load(modNm),
  if cdim = 1 then (
    label:["x", "v", "m"]
  ),
  if cdim = 2 then (
    label:["x", "y", "v", "m"]
  ),
  if cdim = 3 then (
    label:["x", "y", "z", "v", "m"]
  ),
  bC : basisC[i],
  if vdim=0 then bP : bC
  else bP : subst(gkvsub, basisP[i]),
  if vdim=0 then varsP : varsC
  else varsP : subst(gkvsub, varsP),
  numC : length(bC),

  if emflag then printf(fh, "double ~aP~a_Bvars_~a(const double q_, const double m_, const double *w, const double *dxv, const double *Bmag, const double *BmagInv, const double *Gradpar, const double *geoX, const double *geoY, const double *geoZ, const double *Phi, const double *Apar, const double* dApardt, const double *f, double *out) ~%{ ~%", funcNm, i, simplode(bvarslist,"_"))
  else printf(fh, "double ~aP~a_Bvars_~a(const double q_, const double m_, const double *w, const double *dxv, const double *Bmag, const double *BmagInv, const double *Gradpar, const double *geoX, const double *geoY, const double *geoZ, const double *Phi, const double *f, double *out) ~%{ ~%", funcNm, i, simplode(bvarslist,"_")),

  printf(fh, "// w[NDIM]: Cell-center coordinates. dxv[NDIM]: Cell spacing. H/f: Input Hamiltonian/distribution function. out: Incremented output ~%"),
  volfac : 1,
  cellCenter : makelist(varsP[d]=0, d, 1, length(varsP)),
  /* set up ignorable variables for Bmag */
  Bignorevars: flatten(makelist([varsC[d]^2=var2,var2=1/3,varsC[d]=0], d, 1, length(varsC))),
  if bvarslist # [0] then (
    for i : 1 thru length(bvarslist) do (
      Bignorevars : delete( varsC[bvarslist[i]]=0, Bignorevars ),
      Bignorevars : delete( varsC[bvarslist[i]]^2=var2, Bignorevars )
  )),
  /* for grad and curl expressions */
  scalefactors(varsC),
  derivfacsC : makelist(derivfac[d], d, 1, length(varsC)),
  derivfacsV : makelist(derivfac[d], d, 4, 5),
  wC : makelist(wc[d], d, 1, length(varsC)),
  wV : makelist(wc[d], d, 4, 5),

  for dir : 1 thru cdim+vdim do (
    printf(fh, "  double dfac_~a = 2.0/dxv[~a]; ~%", label[dir], dir-1)
  ),

  for dir : 1 thru cdim+vdim do (
    printf(fh, "  double w~a = w[~a]; ~%", label[dir], dir-1)
  ),
  printf(fh, "  double dfac_x2 = dfac_x*dfac_x; ~%"),
  if vdim>0 then (
    printf(fh, "  double wv2 = wv*wv; ~%"),
    printf(fh, "  double dfac_v2 = dfac_v*dfac_v; ~%")
  ),
  printf(fh, "  double q2 = q_*q_; ~%"),
  printf(fh, "  double m2 = m_*m_; ~%"),
  flush_output(fh),

  /* expand background magnetic field Bmag, assuming it only varies in x */
  Bmag_expd:subst(Bignorevars,doExpand1(Bmag,bC)),
  BmagInv_expd:subst(Bignorevars,doExpand1(BmagInv,bC)),
  /* expand Phi */
  Phi_expd:doExpand1(Phi,bC),
  if emflag then (
    /* expand Apar */
    Apar_expd:doExpand1(Apar,bC),
    dAdt_expd:doExpand1(dApardt,bC)
  )
  else (
    Apar_expd : 0,
    dAdt_expd : 0
  ),

  /* calculate full expansion expression for Hamiltonian. */
  H_full: q_*Phi_expd,
  if vdim > 0 then H_full : H_full + 1/2*m_*(wv + vpar/dfac_v)^2,
  if vdim > 1 then H_full : H_full + (wm+mu/dfac_m)*Bmag_expd,

  /* project Hamiltonian onto basis functions */
  H_k:fullratsimp(innerProd(varsP, 1, bP, H_full)),
  /* expand projected Hamiltonian on basis */
  H_expd:H_k.bP,

  /* expand distribution function on basis */
  f_expd:doExpand1(f,bP),

  /* expand geoX, geoY, geoZ on basis */
  geoX_expd : subst(Bignorevars, doExpand1(geoX, bC)),
  geoY_expd : subst(Bignorevars, doExpand1(geoY, bC)),
  geoZ_expd : subst(Bignorevars, doExpand1(geoZ, bC)),

  /* expand BstarX/Bmag = (m/q vpar + Apar)/B*curl(bhat)*grad(x) on basis */
  BstarX_by_Bmag_expd : (m_/q_*(wv+vpar/dfac_v) + Apar_expd)*geoY_expd*BmagInv_expd*dfac_z*diff(Bmag_expd,z) + (geoZ_expd*dfac_y*diff(Apar_expd,y) - geoY_expd*dfac_z*diff(Apar_expd,z)),
  /* expand BstarY/Bmag = (m/q vpar + Apar)/B*curl(bhat)*grad(y) on basis */
  BstarY_by_Bmag_expd : (m_/q_*(wv+vpar/dfac_v) + Apar_expd)*(geoZ_expd*BmagInv_expd*dfac_x*diff(Bmag_expd,x) - geoX_expd*BmagInv_expd*dfac_z*diff(Bmag_expd,z)) + (-geoZ_expd*dfac_x*diff(Apar_expd,x) + geoX_expd*dfac_z*diff(Apar_expd,z)),
  /* expand BstarZ/Bmag = bhat.grad(z) + (m/q vpar + Apar)/B*curl(bhat)*grad(z) = Gradpar + (m/q vpar + Apar)/B*curl(bhat)*grad(z) on basis */
  BstarZ_by_Bmag_expd : subst(Bignorevars, doExpand1(Gradpar, bC)) - (m_/q_*(wv+vpar/dfac_v) + Apar_expd)*geoY_expd*BmagInv_expd*dfac_x*diff(Bmag_expd,x) + (geoY_expd*dfac_x*diff(Apar_expd,x) - geoX_expd*dfac_y*diff(Apar_expd,y)),

  /* make Bstar/Bmag vector */
  if cdim = 1 then Bstar_by_Bmag_expd : [BstarZ_by_Bmag_expd],
  if cdim = 2 then Bstar_by_Bmag_expd : [BstarX_by_Bmag_expd, BstarY_by_Bmag_expd],
  if cdim = 3 then Bstar_by_Bmag_expd : [BstarX_by_Bmag_expd, BstarY_by_Bmag_expd, BstarZ_by_Bmag_expd],

  /* calculate cflFeq = sum_d alpha_d / dxv[d] */
  printf(fh, "  double cflFreq = 0.0; ~%"), 
  printf(fh, "  double alphaL = 0.0; ~%"), 
  printf(fh, "  double alphaR = 0.0; ~%"), 
  
  /* note: no contribution from mu */
  alphaSum_expd : 0,
  if vdim = 0 then vd : 0
  else vd : 1,
  for d : 1 thru cdim+vd do (
    if d>cdim then (
      wD : wV[d-cdim],
      dfac_d : derivfacsV[d-cdim]
    ) 
    else (
      wD : wC[d],
      dfac_d : derivfacsC[d]
    ),
    /* calculate phase space velocity alpha_d = {z[d], H} = dz[d]/dt*/
    alpha_expd : gkPB(wD + varsP[d]/dfac_d, H_expd),

    /* project alpha on basis and write to array */
    printf(fh, "  double alpha~a[~a]; ~%", label[d], length(bP)),
    alpha_k : fullratsimp(innerProd(varsP, 1, bP, alpha_expd)*dfac_d),
    alphalabel : eval_string(sconcat(alpha, label[d])),
    writeCExprsNoExpand1(alphalabel, gcfac(expand(facsum(subst([wv^2=wv2,dfac_v^2=dfac_v2, m_^2=m2, q_^2=q2,dfac_x^2=dfac_x2], alpha_k),derivfac)))),
    flush_output(fh),
    
    alphaNoZero_k : doMakeExprLst(alpha_k, alphalabel),
    alpha_expd : doExpandLst(alphaNoZero_k, bP),

    /* note: factor of dfac_d already included in alpha_expd */
    alphaDotGradBasis_expd : alpha_expd*diff(bP, varsP[d]),

    /* accumulate alpha.grad(w) in this direction to the direction-summed alpha.grad(w) */
    alphaSum_expd : alphaSum_expd + alphaDotGradBasis_expd,

    /* cfl calculation */
    /* ONLY FOR CFL: add on dA/dt term in vpar direction */
    if d = cdim + 1 then alpha_expd : alpha_expd - q_/m_*dAdt_expd*dfac_v,
    /* get quadrature nodes on surface */
    surfIntVars : delete(varsP[d], varsP),
    if i=1 then quad:1/sqrt(3),
    if i=2 then quad:sqrt(3/5),
    nodes : eval_string(sconcat("nodes", cdim+vdim-1, "xp", p)),
    nodesub(f):=makelist(subst(makelist(surfIntVars[j]=nodes[i][j]*quad,j,1,length(surfIntVars)),f),i,1,length(nodes)),

    /* evaluate alpha at left and right edge */
    alphaL : subst(varsP[d]=-1, alpha_expd),
    alphaR : subst(varsP[d]=1, alpha_expd),

    /* evaluate cfl by surface averaging */
    printf(fh, "#if cflType == SURFAVG ~%"),
    /* note: factor of dfac_d already included in alpha_expd */
    alphaL0 : fullratsimp(innerProd(delete(varsP[d],varsP), 1, alphaL, subst(varsP[d]=-1, bP[1]))*bP[1]),
    printf(fh, "  // evaluate surface-averaged alpha on left ~%"),
    printf(fh, "  alphaL = ~a; ~%", gcfac(float(alphaL0))),
    printf(fh, "  cflFreq += -0.5*(alphaL-std::abs(alphaL)); ~%"),
    /* note: factor of dfac_d already included in alpha_expd */
    alphaR0 : fullratsimp(innerProd(delete(varsP[d],varsP), 1, alphaR, subst(varsP[d]=1, bP[1]))*bP[1]),
    printf(fh, "  // evaluate surface-averaged alpha on right ~%"),
    printf(fh, "  alphaR = ~a; ~%", gcfac(float(alphaR0))),
    printf(fh, "  cflFreq += 0.5*(alphaR+std::abs(alphaR)); ~%"),
    flush_output(fh),

    /* evaluate cfl by evaluating at all surface quadrature points */
    printf(fh, "#elif cflType == QUAD ~%"),
    alphaLquad : nodesub(alphaL*subst(varsP[d]=-1,bP[1]))*bP[1],
    alphaRquad : nodesub(alphaR*subst(varsP[d]=1,bP[1]))*bP[1],
    printf(fh, "  // evaluate alpha at left surface quadrature points ~%"),
    for i:1 thru length(alphaLquad) do (
      printf(fh, "  alphaL = ~a; ~%", gcfac(float(alphaLquad[i]))),
      printf(fh, "  cflFreq += -0.5*(alphaL-std::abs(alphaL)); ~%")
    ),
    printf(fh, "  // evaluate alpha at right surface quadrature points ~%"),
    for i:1 thru length(alphaRquad) do (
      printf(fh, "  alphaR = ~a; ~%", gcfac(float(alphaRquad[i]))),
      printf(fh, "  cflFreq += 0.5*(alphaR+std::abs(alphaR)); ~%")
    ),
    printf(fh, "#endif ~%"),

    printf(fh, "~%")
    
  ),
  gkVolTerm_k : fullratsimp(innerProd(varsP, 1, f_expd, alphaSum_expd)),
  writeCIncrExprsNoExpand(gcfac(float(subst([wv^2=wv2,dfac_v^2=dfac_v2, m_^2=m2, q_^2=q2], facsum(gkVolTerm_k,derivfac))))),
  flush_output(fh),

  printf(fh, "  return cflFreq; ~%"),
  printf(fh, "} ~%"),
  flush_output(fh)
)$

calcGKStep2VolUpdater(fh, funcNm, cdim, vdim, basisFun, i) := block([],
kill(varsC, varsP, basisC, basisP),
modNm : sconcat("basis-precalc/basis", basisFun, cdim, "x", vdim, "v"),
load(modNm),
  if cdim = 1 then (
    label:["x", "v", "m"]
  ),
  if cdim = 2 then (
    label:["x", "y", "v", "m"]
  ),
  if cdim = 3 then (
    label:["x", "y", "z", "v", "m"]
  ),
  bP : subst(gkvsub, basisP[i]),
  bC : basisC[i],
  varsP : subst(gkvsub, varsP),
  numC : length(bC),

  printf(fh, "double ~aP~a(const double q_, const double m_, const double *w, const double *dxv, const double *dApardt, const double *f, double *out) ~%{ ~%", funcNm, i),

  printf(fh, "  double dvInv = 1.0/dxv[~a]; ~%", cdim),
  printf(fh, "  double dfac_v = 2.0/dxv[~a]; ~%", cdim),

  /* expand dApar/dt */
  dAdt_expd:doExpand1(dApardt,bC),

  /* calculate volume term, which is inner product
     < - f q/m dApar/dt d/dvpar(bP) > */
  gkVolTerm_k : fullratsimp(innerProd(varsP, 1, f_expd, -q_/m_*dAdt_expd*diff(bP,vpar)*dfac_v)),

  /* write C increment expressions for volume term */
  writeCIncrExprsNoExpand(float(subst([wv^2=wv2,dfac_v^2=dfac_v2, m_^2=m2, q_^2=q2], facsum(gkVolTerm_k,derivfac)))),

  /* cfl calculation */
  /* cfl contribution from this term handled in first step, so just return 0 */
  printf(fh, "  return 0.0; ~%"),
  printf(fh, "} ~%")
)$

pmax:1$
em:false$

for c : 1 thru 3 do (
for v: 1 thru 2 do (
if c=1 or (c>1 and v=2) then (
for p : 1 thru pmax do (
if em then fname : sconcat("~/max-out/EmGyrokineticSer", c, "x", v, "vP", p, ".cpp")
else fname : sconcat("~/max-out/GyrokineticSer", c, "x", v, "vP", p, ".cpp"),
fh : openw(fname),
printf(fh, "#include <GyrokineticModDecl.h> ~%"),
/* possible combinations of variable dependence of background magnetic field,
   with 0 = const, 1 = x, 2 = y, 3 = z.
   note that we assume axisymmetry, which means B cannot depend on y */
if c=1 then bvarslist : [[0], [1]],
if c=2 then bvarslist : [[0], [1]],
if c=3 then bvarslist : [[0], [1], [3], [1,3]],
for b : 1 thru length(bvarslist) do (
if em then (
funcName : sconcat("EmGyrokineticVol", c, "x", v, "v", "Ser"),
calcGKVolUpdater(fh, funcName, c, v, "Ser", p, true, bvarslist[b]),
funcName : sconcat("EmGyrokineticStep2Vol", c, "x", v, "v", "Ser"),
if b=1 then calcGKStep2VolUpdater(fh, funcName, c, v, "Ser", p)
) 
else (
funcName : sconcat("GyrokineticVol", c, "x", v, "v", "Ser"),
calcGKVolUpdater(fh, funcName, c, v, "Ser", p, false, bvarslist[b])
)),
close(fh)
))))$

if em=false then (
c:2,
v:0,
for p : 1 thru pmax do (
fname : sconcat("~/max-out/GyrokineticSer", c, "x", v, "vP", p, ".cpp"),
fh : openw(fname),
printf(fh, "#include <GyrokineticModDecl.h> ~%"),
bvarslist : [[0]],
funcName : sconcat("GyrokineticVol", c, "x", v, "v", "Ser"),
calcGKVolUpdater(fh, funcName, c, v, "Ser", p, false, [0]),
close(fh)
)
)$

