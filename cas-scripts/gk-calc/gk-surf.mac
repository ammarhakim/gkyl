load("modal-basis")$
load("out-scripts")$
load(stringproc)$
load("positivity-calc/fhatAL-calc")$
load("scifac")$
fpprec : 24$

wc : [wx, wy, wz, wv, wm]$
wc2 : [wx2, wy2, wz2, wv2, wm2]$
derivfac : [dfac_x, dfac_y, dfac_z, dfac_v, dfac_m]$
derivfac2 : [dfac_x2, dfac_y2, dfac_z2, dfac_v2, dfac_m2]$
gkvsub:[vx=vpar,vy=mu]$
gkvsubInv:[vpar=vx,mu=vy]$

let (wv^2, wv2)$
let (dfac_v^2, dfac_v2)$

cvars : [X, Y, Z]$
vvars : [Vpar, Mu]$

doMakeExprLst(vals, S) := makelist(if vals[i] # 0 then S[i-1] else 0, i, 1, length(vals))$
doExpandLst(lst, basis) := sum(lst[i]*basis[i], i, 1, length(basis))$

/* set up some vector calculus functions */
load("vect")$
fgrad(f):= ev(express(grad(f)),diff)*derivfacsC$

positivityFluxLimiter(outl_c, outr_c, vars, basis, polyOrder, surfDir, fl_e, fr_e) := block(
  /* local variables for this function */
  [outl_e, outr_e, ndim, nodes, surfNodes, posProjBasis, posSurfProjBasis, indP, indM],

  /* Expand outl and outr on volume basis */
  outl_e : outl_c.basis,
  outr_e : outr_c.basis,

  ndim : length(vars),
 
  /* Get positivity surface projection basis */
  nodes : eval_string(sconcat("nodes", ndim, "xp", polyOrder)),
  posProjBasis : fullratsimp(getNodalBasis(nodes, basis, vars)),
  if ndim>1 then surfNodes : eval_string(sconcat("nodes", ndim-1, "xp", polyOrder))
  else surfNodes : [[1]],

  indP : sublist_indices(args(nodes), lambda([x],x[surfDir]=1)),
  indM : sublist_indices(args(nodes), lambda([x],x[surfDir]=-1)),

  /* Project outl and fl onto positivity basis */
  fl_pos : gcfac(float(calcInnerProdList(vars, 1, posProjBasis, fl_e))),
  fl_posP : makelist(fl_pos[indP[i]],i,1,length(indP)),

  outl_pos : gcfac(float(calcInnerProdList(vars, 1, posProjBasis, outl_e))),
  outl_posP : makelist(outl_pos[indP[i]],i,1,length(indP)),
  outl_posM : makelist(outl_pos[indM[i]],i,1,length(indM)),

  /* Project outr and fr onto positivity basis */
  fr_pos : gcfac(float(calcInnerProdList(vars, 1, posProjBasis, fr_e))),
  fr_posM : makelist(fr_pos[indM[i]],i,1,length(indM)),

  outr_pos : gcfac(float(calcInnerProdList(vars, 1, posProjBasis, outr_e))),
  outr_posP : makelist(outr_pos[indP[i]],i,1,length(indP)),
  outr_posM : makelist(outr_pos[indM[i]],i,1,length(indM)),

  /* Set up positivity weights */
  printf(fh, "  double fluxFracL, fluxFracR, limFac=1.0;~%"),
  printf(fh, "  double outlPos[~a], outrPos[~a]; ~%", length(posProjBasis), length(posProjBasis)),
  printf(fh, "  fluxFracL = positivityWeightByDirL[0] == 0. ? ~a : positivityWeightByDirL[~a]/positivityWeightByDirL[0]; ~%", float(1/(ndim)), surfDir),
  printf(fh, "  fluxFracR = positivityWeightByDirR[0] == 0. ? ~a : positivityWeightByDirR[~a]/positivityWeightByDirR[0]; ~%", float(1/(ndim)), surfDir),

  /* Write coefficients of outl and outr on positivity basis */
  writeCExprsNoExpand1(outlPos, outl_pos),
  writeCExprsNoExpand1(outrPos, outr_pos),
  flush_output(fh),

  /* Loop over positivity surface basis, and calculate limiters */
  for i : 1 thru length(surfNodes) do (
     printf(fh, "  if(outlPos[~a] < EPSILON) limFac = std::min(1.0, -fluxFracL*(~a)/dtApprox/outlPos[~a]); ~%", indP[i]-1, fl_posP[i], indP[i]-1),
     printf(fh, "  else limFac = 1.0; ~%"),
     printf(fh, "  if(outrPos[~a] < EPSILON) limFac = std::min(limFac, -fluxFracR*(~a)/dtApprox/outrPos[~a]); ~%", indM[i]-1, fr_posM[i], indM[i]-1),
     printf(fh, "  if(limFac < 0.) limFac = 0.; ~%"),
     
     printf(fh, "  outlPos[~a] *= limFac; ~%", indP[i]-1),
     printf(fh, "  outlPos[~a] *= limFac; ~%", indM[i]-1),
     printf(fh, "  outrPos[~a] *= limFac; ~%", indP[i]-1),
     printf(fh, "  outrPos[~a] *= limFac; ~%", indM[i]-1),
     flush_output(fh)
  ),

  /* Get positivity surface expansion basis */
  posExpdBasis:makelist(nodesub(basis, vars, nodes)[i].basis,i,1,length(basis)),

  /* Expand on positivity surface expansion basis, and then project back onto modal basis */
  outl_pos_e : doExpand1(outlPos,posExpdBasis),
  outr_pos_e : doExpand1(outrPos,posExpdBasis),

  outl_c : gcfac(float(fullratsimp(calcInnerProdList(vars, 1, basis, outl_pos_e)))),
  outr_c : gcfac(float(fullratsimp(calcInnerProdList(vars, 1, basis, outr_pos_e)))),
  [outl_c, outr_c]
)$

gkPB(f,g):=1/m_*Bstar_by_Bmag_expd.(fgrad(f)*dfac_v*diff(g,vpar)-fgrad(g)*dfac_v*diff(f,vpar))-1/q_*BmagInv_expd*dfac_x*dfac_y*(diff(f,x)*diff(g,y)-diff(f,y)*diff(g,x))$

calcUpdateInDir(surfDir, fh, funcNm, cdim, vdim, basisFun, polyOrder, emflag, bvarslist, pureUpwind) := block([],
kill(varsC, varsP, basisC, basisP),
if vdim > 0 then modNm : sconcat("basis-precalc/basis", basisFun, cdim, "x", vdim, "v")
else modNm : sconcat("basis-precalc/basis", basisFun, cdim, "x"),
load(modNm),
  if cdim = 1 then (
    label:["x", "v", "m"]
  ),
  if cdim = 2 then (
    label:["x", "y", "v", "m"]
  ),
  if cdim = 3 then (
    label:["x", "y", "z", "v", "m"]
  ),
  bC : basisC[polyOrder],
  if vdim=0 then bP : bC
  else bP : subst(gkvsub, basisP[polyOrder]),
  if vdim=0 then varsP : varsC
  else varsP : subst(gkvsub, varsP),
  numC : length(bC),
  volfac : 1,
  /* get surface variable */
  surfVar : varsP[surfDir],
  /* remove surface variable from integrand of surface integrals */
  surfIntVars : delete(surfVar,varsP),
  if surfDir <= cdim then dirlabel : cvars[surfDir]
  else dirlabel : vvars[surfDir-cdim],
  cellCenter : makelist(varsP[d]=0, d, 1, length(varsP)),
  /* set up ignorable variables for Bmag */
  Bignorevars: flatten(makelist([varsC[d]^2=var2,var2=1/3,varsC[d]=0], d, 1, length(varsC))),
  if bvarslist # [0] then (
    for i : 1 thru length(bvarslist) do (
      Bignorevars : delete( varsC[bvarslist[i]]=0, Bignorevars ),
      Bignorevars : delete( varsC[bvarslist[i]]^2=var2, Bignorevars )
  )),
  /* for grad and curl expressions */
  scalefactors(varsC),
  derivfacsC : makelist(derivfac[d], d, 1, length(varsC)),
  derivfacsV : makelist(derivfac[d], d, 4, 5),
  wC : makelist(wc[d], d, 1, length(varsC)),
  wV : makelist(wc[d], d, 4, 5),
  surfLabel : label[surfDir],
  if surfDir <= cdim then (dfacSurf : derivfacsC[surfDir], wSurf : wC[surfDir])
  else (dfacSurf : derivfacsV[surfDir-cdim], wSurf : wV[surfDir-cdim]),

  /* remove volume contribution from surface variable, which is not integrated */
  surfac : fullratsimp(volfac*dfacSurf),

  print("Working on ", funcNm, "_", dirlabel, "_P", polyOrder, "_Bvars_", simplode(bvarslist,"_")),
  if emflag then (
    if positivity then 
      printf(fh, "double ~a_~a_P~a_Bvars_~a(const double q_, const double m_, const double *w, const double *dxv, const double amax_in,
                        const double *Bmag, const double *BmagInv, const double *Gradpar, 
                        const double *BdriftX, const double *BdriftY, const double *Phi, 
                        const double *Apar, const double *dApardt,
                        const double dtApprox, const double *positivityWeightByDirL, const double *positivityWeightByDirR, 
                        const double *fl, const double *fr, double *outl, double *outr) ~%{ ~%",
                  funcNm, dirlabel, polyOrder, simplode(bvarslist,"_"))
    else 
      printf(fh, "double ~a_~a_P~a_Bvars_~a(const double q_, const double m_, const double *w, const double *dxv, const double amax_in, 
                        const double *Bmag, const double *BmagInv, const double *Gradpar, 
                        const double *BdriftX, const double *BdriftY, const double *Phi, 
                        const double *Apar, const double *dApardt, const double *dApardtProv, 
                        const double *fl, const double *fr, double *outl, double *outr, 
                        double *ohmModL, double *ohmModR) ~%{ ~%", 
                  funcNm, dirlabel, polyOrder, simplode(bvarslist,"_"))
  )
  else (
    if positivity then
      printf(fh, "double ~a_~a_P~a_Bvars_~a(const double q_, const double m_, const double *w, const double *dxv, const double amax_in, 
                        const double *Bmag, const double *BmagInv, const double *Gradpar, 
                        const double *BdriftX, const double *BdriftY, const double *Phi, 
                        const double dtApprox, const double *positivityWeightByDirL, const double *positivityWeightByDirR, 
                        const double *fl, const double *fr, double *outl, double *outr) ~%{ ~%", 
                  funcNm, dirlabel, polyOrder, simplode(bvarslist,"_"))
    else
      printf(fh, "double ~a_~a_P~a_Bvars_~a(const double q_, const double m_, const double *w, const double *dxv, const double amax_in, 
                        const double *Bmag, const double *BmagInv, const double *Gradpar, 
                        const double *BdriftX, const double *BdriftY, const double *Phi, 
                        const double *fl, const double *fr, double *outl, double *outr) ~%{ ~%", 
                  funcNm, dirlabel, polyOrder, simplode(bvarslist,"_"))
  ),
  printf(fh, "// w[NDIM]: Cell-center coordinates. dxv[NDIM]: Cell spacing. H/f: Input Hamiltonian/distribution function. out: Incremented output ~%"),

  for dir : 1 thru cdim+vdim do (
    printf(fh, "  double dfac_~a = 2.0/dxv[~a]; ~%", label[dir], dir-1)
  ),

  for dir : 1 thru cdim+vdim do (
    printf(fh, "  double w~a = w[~a]; ~%", label[dir], dir-1)
  ),
  if vdim>0 then (
    printf(fh, "  double wv2 = wv*wv; ~%"),
    printf(fh, "  double dfac_v2 = dfac_v*dfac_v; ~%")
  ),
  printf(fh, "  double q2 = q_*q_; ~%"),
  printf(fh, "  double incr[~a]; ~%", length(bP)),
  if emflag and surfVar=vpar and polyOrder=1 then printf(fh, "  double incrOhmMod[~a]; ~%", length(bP)),
  flush_output(fh),

  /* expand background magnetic field Bmag, assuming it only varies in x */
  Bmag_expd:subst(Bignorevars,doExpand1(Bmag,bC)),
  BmagInv_expd:subst(Bignorevars,doExpand1(BmagInv,bC)),
  /* expand Phi */
  Phi_expd:doExpand1(Phi,bC),

  if emflag then (
    /* expand Apar */
    Apar_expd:doExpand1(Apar,bC),
    /* expand dApar/dt */
    dAdt_expd:doExpand1(dApardt,bC),
    dAdtProv_expd:doExpand1(dApardtProv,bC)
    /* NOTE: even though Apar and dApar/dt are allowed
       to be discontinuous in the parallel (z) direction,
       the surface term in the z direction does not involve
       Apar. since Apar is continuous in the other directions,
       it does not matter if we use the right or left value */
  )
  else (
    Apar_expd : 0,
    dAdt_expd : 0,
    dAdtProv_expd : 0
  ),

  /* calculate full expansion expression for Hamiltonian */
  H_full: q_*Phi_expd,
  if vdim > 0 then H_full : H_full + 1/2*m_*(wv + vpar/dfac_v)^2,
  if vdim > 1 then H_full : H_full + (wm+mu/dfac_m)*Bmag_expd,
  
  /* project Hamiltonian onto basis functions */
  H_k:fullratsimp(innerProd(varsP, 1, bP, H_full)),
  /* expand projected Hamiltonian on basis */
  H_expd:H_k.bP,

  /* expand distribution function on basis */
  fL_expd:doExpand1(fl,bP),
  fR_expd:doExpand1(fr,bP),

  /* expand BstarZ/Bmag = bhat.grad(z) = Gradpar on basis */
  BstarZ_by_Bmag_expd : subst(Bignorevars, doExpand1(Gradpar, bC)),

  /* expand BdriftX = 1/B*curl(bhat)*grad(x) on basis */
  BdriftX_expd : subst(Bignorevars, doExpand1(BdriftX, bC)),
  /* expand BdriftY = 1/B*curl(bhat)*grad(y) on basis */
  BdriftY_expd : subst(Bignorevars, doExpand1(BdriftY, bC)),
  /* expand BstarX/Bmag = ( m/q vpar + Apar ) BdriftX = (m/q vpar + Apar)/B*curl(bhat)*grad(x) on basis */
  BstarX_by_Bmag_expd : (m_/q_*(wv+vpar/dfac_v) + Apar_expd)*BdriftX_expd,
  /* expand BstarY/Bmag = ( m/q vpar + Apar ) BdriftY = (m/q vpar + Apar)/B*curl(bhat)*grad(y) on basis */
  BstarY_by_Bmag_expd : (m_/q_*(wv+vpar/dfac_v) + Apar_expd)*BdriftY_expd,
  /* make vApar = -1/B*bhatXgrad(Apar) vector */
  if cdim = 1 then vApar_expd : [0], 
  if cdim = 2 then vApar_expd : [diff(Apar_expd, y)*dfac_y, -diff(Apar_expd,x)*dfac_x]*BmagInv_expd,
  if cdim = 3 then vApar_expd : [diff(Apar_expd, y)*dfac_y, -diff(Apar_expd,x)*dfac_x, 0]*BmagInv_expd, 

  /* make Bstar/Bmag vector */
  if cdim = 1 then Bstar_by_Bmag_expd : [BstarZ_by_Bmag_expd],
  if cdim = 2 then Bstar_by_Bmag_expd : [BstarX_by_Bmag_expd, BstarY_by_Bmag_expd] + vApar_expd,
  if cdim = 3 then Bstar_by_Bmag_expd : [BstarX_by_Bmag_expd, BstarY_by_Bmag_expd, BstarZ_by_Bmag_expd] + vApar_expd,

  /* calculate phase space velocity alpha in direction of surfVar */
  /* we assume alpha.n is continuous across boundary, although H may not be */
  /* distinguish between alpha and alphaUp, where alphaUp is the one used to determine upwind direction */
  alpha_expd : gkPB(wSurf + surfVar/dfacSurf, H_expd),
  alphaUp_expd : alpha_expd,
  if surfVar = vpar and (polyOrder > 1 or positivity) then (
    alpha_expd : alpha_expd - q_/m_*dAdt_expd,
    alphaUp_expd : alpha_expd
  ) elseif surfVar = vpar then (
    /* for p=1 EMGK, use dAdtProv in upwinding alpha */
    alphaUp_expd : alpha_expd - q_/m_*dAdtProv_expd
  ),
  /* calculate expression for surface-averaged alpha, and print to c variable alpha0 */
  alpha0expr : gcfac(fullratsimp(innerProd(surfIntVars, 1, subst(surfVar=-1,alphaUp_expd), subst(surfVar=-1, bP[1]))*bP[1])),
  printf(fh, "  // surface-averaged phase velocity in this direction ~%"),
  printf(fh, "  double alpha0 = ~a; ~%~%", float(subst([wv^2=wv2,q_^2=q2,dfac_v^2=dfac_v2], alpha0expr))),
  flush_output(fh),

  /* generate surface basis */
  bSurf : sqrt(2)*delete(0,fullratsimp(innerProd(varsP,1,bP,subst([surfVar^2=var2,var2=1/3,surfVar=0],bP)))*bP),

  /* project full alpha expression evaluatated at interior surface onto surface basis and print to c variable alpha */
  printf(fh, "  double alpha[~a]; ~%", length(bSurf)),
  alpha_k : fullratsimp(innerProd(surfIntVars, 1, bSurf, subst(surfVar=-1,alpha_expd))),
  flush_output(fh),
  writeCExprsNoExpand1(alpha, subst([wv^2=wv2,dfac_v^2=dfac_v2, m_^2=m2, q_^2=q2],alpha_k)),
  alphaNoZero_k : doMakeExprLst(alpha_k, alpha),
  alphaSurf_expd : doExpandLst(alphaNoZero_k, bSurf),
  flush_output(fh),

  if surfVar=vpar and emflag and (polyOrder=1 and positivity=false) then (
    /* print alphaUp */
    printf(fh, "  double alphaUp[~a]; ~%", length(bSurf)),
    alphaUp_k : fullratsimp(innerProd(surfIntVars, 1, bSurf, subst(surfVar=-1,alphaUp_expd))),
    flush_output(fh),
    writeCExprsNoExpand1(alphaUp, subst([wv^2=wv2,dfac_v^2=dfac_v2, m_^2=m2, q_^2=q2],alphaUp_k)),
    alphaUpNoZero_k : doMakeExprLst(alphaUp_k, alphaUp),
    alphaUpSurf_expd : doExpandLst(alphaUpNoZero_k, bSurf),
    flush_output(fh)
  ) else (
    alphaUpSurf_expd : alphaSurf_expd
  ),

  /* note: always use pureUpwind for p=1 */
  if (alphaSurf_expd = 0) then (
    printf(fh, "  // alpha == 0, so nothing to do ~%"),
    printf(fh, "  return std::abs(alpha0); ~%"),
    printf(fh, "} ~%"),
    flush_output(fh)
  )
  else if pureUpwind or polyOrder=1 then (
    printf(fh, "#if upwindType == SURFAVG ~%"),
    printf(fh, "  if (alpha0>0) { ~%"),
    fhatSurf_expd : subst(surfVar=1, fL_expd),

    t : surfac*calcInnerProdList(surfIntVars, alphaSurf_expd, subst(surfVar=-1, bP), fhatSurf_expd),
    incr_r : facsum(fullratsimp(t),dxv),
    writeCExprsNoExpand1(incr, subst([wv^2=wv2,q_^2=q2], incr_r)),
    flush_output(fh),

      if surfVar=vpar and emflag and polyOrder=1 then (
         tMod : surfac*calcInnerProdList(surfIntVars, 1, subst(surfVar=-1, bP), fhatSurf_expd),
         incrMod_r : facsum(fullratsimp(tMod),dxv),
         writeCExprsNoExpand1(incrOhmMod, subst([wv^2=wv2,q_^2=q2], incrMod_r))
      ),

    printf(fh, "  } else { ~%"),
    fhatSurf_expd : subst(surfVar=-1, fR_expd),

      if surfVar=vpar and emflag and polyOrder=1 then (
         tMod : surfac*calcInnerProdList(surfIntVars, 1, subst(surfVar=-1, bP), fhatSurf_expd),
         incrMod_r : facsum(fullratsimp(tMod),dxv),
         writeCExprsNoExpand1(incrOhmMod, subst([wv^2=wv2,q_^2=q2], incrMod_r))
      ),

    t : surfac*calcInnerProdList(surfIntVars, alphaSurf_expd, subst(surfVar=-1, bP), fhatSurf_expd),
    incr_r : facsum(fullratsimp(t),dxv),
    writeCExprsNoExpand1(incr, subst([wv^2=wv2,q_^2=q2], incr_r)),
    printf(fh, "  }~%"),
    flush_output(fh),

    printf(fh, "#elif upwindType == QUAD ~%"),
    if(positivity) then (
      fhatSurf_expd : subst(gkvsub, fhatALgenUpwindNoLim(subst(gkvsubInv,fL_expd), 
              subst(gkvsubInv,fR_expd), subst(gkvsubInv,alphaUpSurf_expd), 
              subst(gkvsubInv,surfVar), cflL, cflR, cdim, vdim, polyOrder))
    ) else (
      printf(fh, "double fupwind[~a];~%", length(bSurf)),
      printf(fh, "double fupwindQuad[~a];~%", length(bSurf)),
      printf(fh, "double alphaQuad;~%"),
      if polyOrder=1 then quad:1/sqrt(3),
      if polyOrder=2 then quad:sqrt(3/5),
      nodes : eval_string(sconcat("nodes", cdim+vdim-1, "xp", polyOrder))*quad,
      /* evaluate fl, fr, and alpha at surface quadrature nodes */
      frQ:gcfac(float(nodesub(subst(surfVar=-1,fR_expd),surfIntVars,nodes))),
      flQ:gcfac(float(nodesub(subst(surfVar=1,fL_expd),surfIntVars,nodes))), 
      /* note: alphaSurf_expd is already a surface expansion */
      alphaq:gcfac(float(nodesub(alphaUpSurf_expd,surfIntVars,nodes))),
      /* determine upwinding at each surface quadrature node */
      fupQ : gcfac(.5*(flQ+frQ)+.5*sgn(alphaQuad)*(flQ-frQ)),
      for i:1 thru length(alphaq) do (
      printf(fh, "  alphaQuad = ~a; ~%", alphaq[i]),
      printf(fh, "  fupwindQuad[~a] = ~a; ~%", i-1, fupQ[i])
      ),
      flush_output(fh),
      /* use modToNod transformation to get modal surface expansion from nodal quadrature points */
      modToNod:calcModToNodPhaseWithNodesAndBasis(nodes,bSurf,surfIntVars),
      fupwind_k:gcfac(fullratsimp(invert(transpose(modToNod)).makelist(fupwindQuad[i-1],i,1,length(nodes)))),
      fupwind_k:makelist(fupwind_k[i][1],i,1,length(fupwind_k)),
      /* write coefficients of modal surface expansion fupwind */
      writeCExprsNoExpand1(fupwind, fupwind_k),
      fhatSurf_expd:doExpand1(fupwind,bSurf)
    ),
    flush_output(fh),

       if surfVar=vpar and emflag and polyOrder=1 then (
          tMod : surfac*calcInnerProdList(surfIntVars, 1, subst(surfVar=-1, bP), fhatSurf_expd),
          incrMod_r : facsum(fullratsimp(tMod),dxv),
          writeCExprsNoExpand1(incrOhmMod, subst([wv^2=wv2,q_^2=q2], incrMod_r))
       ),

    t : surfac*calcInnerProdList(surfIntVars, alphaSurf_expd, subst(surfVar=-1, bP), fhatSurf_expd),
    incr_r : facsum(fullratsimp(t),dxv),
    writeCExprsNoExpand1(incr, subst([q_^2=q2], incr_r)),
    printf(fh, "~%"),
    flush_output(fh),
    printf(fh, "#endif ~%"),

    incr_s : makelist(incr[i-1], i, 1, length(bP)),
    for i : 1 thru length(bP) do (
      if (incr_r[i] = 0) then incr_s[i] : 0
    ),
    outr_c : incr_s,
    t : surfac*calcInnerProdList(surfIntVars, alphaSurf_expd, subst(surfVar=1, bP), fhatSurf_expd),
    incr_l : -t,
    signs : fullratsimp(incr_l/incr_r),
    incr_s : makelist(signs[i]*incr[i-1], i, 1, length(bP)),
    for i : 1 thru length(bP) do (
      if (incr_r[i] = 0) then incr_s[i] : 0
    ),
    outl_c : incr_s,

    if (positivity) then (
      [outl_c, outr_c] : positivityFluxLimiter(outl_c, outr_c, varsP, bP, polyOrder, surfDir, fL_expd, fR_expd)
    ), 

    writeCIncrExprsNoExpand1(outr, outr_c),  
    printf(fh, "~%"),
    flush_output(fh),
    writeCIncrExprsNoExpand1(outl, outl_c),
    flush_output(fh),

    if surfVar=vpar and emflag and polyOrder=1 then (
      incrOhmMod_s : makelist(incrOhmMod[i-1], i, 1, length(bP)),
      writeCIncrExprsNoExpand1(ohmModR, incrOhmMod_s),  
      incrOhmMod_s : makelist(signs[i]*incrOhmMod[i-1], i, 1, length(bP)),
      writeCIncrExprsNoExpand1(ohmModL, incrOhmMod_s),  
      flush_output(fh)
    ),

    printf(fh, "  return std::abs(alpha0); ~%"),
    printf(fh, "} ~%"),
    flush_output(fh)
    
  ) else (
    /* determine whether to use local or global value for penalty amax */
    printf(fh, "  double amax = 0.0; ~%"),
    printf(fh, "  bool upwind = false; ~%"), 
    printf(fh, "  if(upwind) ~%"),
    printf(fh, "    amax = std::abs(alpha0); ~%"),
    printf(fh, "  else ~%"),
    printf(fh, "    amax = amax_in; ~%~%"),
    printf(fh, "  double Ghat[~a]; ~%", length(bSurf)),
    printf(fh, "  double favg[~a]; ~%", length(bSurf)),

    /* print C exprs for favg = fr(sv=-1) + fl(sv=1) projected onto surface basis */
    favg_k : gcfac(fullratsimp(innerProd(surfIntVars, 1, bSurf, subst(surfVar=-1, fR_expd)+subst(surfVar=1, fL_expd)))),
    writeCExprsNoExpand1(favg, favg_k),
    favgNoZero_k : doMakeExprLst(favg_k, favg),
    favg_expd : doExpandLst(favgNoZero_k, bSurf),
    flush_output(fh),

    /* project fjump = fr(sv=-1) - fl(sv=1) onto surface basis. don't need to print it out because it is just part of Ghat */
    fjump_k : gcfac(fullratsimp(innerProd(surfIntVars, 1, bSurf, subst(surfVar=-1, fR_expd)-subst(surfVar=1, fL_expd)))),
    printf(fh, "~%"),
    flush_output(fh),

    /* evaluate surface projection of Ghat = (alpha*favg - amax*fjump)/2 */
    t_k : gcfac(innerProd(surfIntVars, favg_expd, bSurf, alphaSurf_expd)),
    flush_output(fh),
    Ghat_k : fullratsimp(t_k/2 - amax/2*fjump_k),
    flush_output(fh),
    writeCExprsNoExpand1(Ghat, Ghat_k),
    flush_output(fh),
    Ghat_expd : doExpand1(Ghat, bSurf),

    /* evaluate contribution to right boundary (surfVar=-1) */
    incr_r : gcfac(fullratsimp(surfac*innerProd(surfIntVars, 1, subst(surfVar=-1, bP), Ghat_expd))),
    writeCExprsNoExpand1(incr, incr_r),
    printf(fh, "~%"),
    incr_s : makelist(incr[i-1], i, 1, length(bP)),
    writeCIncrExprsNoExpand1(outr, incr_s),  
    printf(fh, "~%"),
    flush_output(fh),
    /* contribution to left boundary (surfVar=1) is same except for signs */
    incr_l : gcfac(fullratsimp(surfac*innerProd(surfIntVars, -1, subst(surfVar=1, bP), Ghat_expd))),
    signs : fullratsimp(incr_l/incr_r),
    incr_s : makelist(signs[i]*incr[i-1], i, 1, length(bP)),
    writeCIncrExprsNoExpand1(outl, incr_s),
    /* return alpha0 so that we can find global value */
    printf(fh, "return std::abs(alpha0); ~%"),
    
    printf(fh, "} ~%"),
    flush_output(fh)
  )
)$

calcStep2Update(fh, funcNm, cdim, vdim, basisFun, polyOrder, emflag, bvarslist, pureUpwind) := block([],
kill(varsC, varsP, basisC, basisP),
surfDir:cdim+1,
if vdim > 0 then modNm : sconcat("basis-precalc/basis", basisFun, cdim, "x", vdim, "v")
else modNm : sconcat("basis-precalc/basis", basisFun, cdim, "x"),
load(modNm),
  if cdim = 1 then (
    label:["x", "v", "m"]
  ),
  if cdim = 2 then (
    label:["x", "y", "v", "m"]
  ),
  if cdim = 3 then (
    label:["x", "y", "z", "v", "m"]
  ),
  bC : basisC[polyOrder],
  if vdim=0 then bP : bC
  else bP : subst(gkvsub, basisP[polyOrder]),
  if vdim=0 then varsP : varsC
  else varsP : subst(gkvsub, varsP),
  numC : length(bC),
  volfac : 1,
  /* get surface variable */
  surfVar : varsP[surfDir],
  /* remove surface variable from integrand of surface integrals */
  surfIntVars : delete(surfVar,varsP),
  if surfDir <= cdim then dirlabel : cvars[surfDir]
  else dirlabel : vvars[surfDir-cdim],
  cellCenter : makelist(varsP[d]=0, d, 1, length(varsP)),
  /* set up ignorable variables for Bmag */
  Bignorevars: flatten(makelist([varsC[d]^2=var2,var2=1/3,varsC[d]=0], d, 1, length(varsC))),
  if bvarslist # [0] then (
    for i : 1 thru length(bvarslist) do (
      Bignorevars : delete( varsC[bvarslist[i]]=0, Bignorevars ),
      Bignorevars : delete( varsC[bvarslist[i]]^2=var2, Bignorevars )
  )),
  /* for grad and curl expressions */
  scalefactors(varsC),
  derivfacsC : makelist(derivfac[d], d, 1, length(varsC)),
  derivfacsV : makelist(derivfac[d], d, 4, 5),
  wC : makelist(wc[d], d, 1, length(varsC)),
  wV : makelist(wc[d], d, 4, 5),
  surfLabel : label[surfDir],
  if surfDir <= cdim then (dfacSurf : derivfacsC[surfDir], wSurf : wC[surfDir])
  else (dfacSurf : derivfacsV[surfDir-cdim], wSurf : wV[surfDir-cdim]),

  /* remove volume contribution from surface variable, which is not integrated */
  surfac : fullratsimp(volfac*dfacSurf),

  printf(fh, "double ~aStep2_~a_P~a_Bvars_~a(const double q_, const double m_, const double *w, const double *dxv, const double amax_in,
                    const double *Bmag, const double *BmagInv, const double *Gradpar, 
                    const double *BdriftX, const double *BdriftY, const double *Phi, 
                    const double *Apar, const double *dApardt, const double *dApardtProv, 
                    const double *fl, const double *fr, double *outl, double *outr) ~%{ ~%", funcNm, dirlabel, polyOrder, simplode(bvarslist,"_")),
  printf(fh, "// w[NDIM]: Cell-center coordinates. dxv[NDIM]: Cell spacing. H/f: Input Hamiltonian/distribution function. out: Incremented output ~%"),

  for dir : 1 thru cdim+vdim do (
    printf(fh, "  double dfac_~a = 2.0/dxv[~a]; ~%", label[dir], dir-1)
  ),

  for dir : 1 thru cdim+vdim do (
    printf(fh, "  double w~a = w[~a]; ~%", label[dir], dir-1)
  ),
  if vdim>0 then (
    printf(fh, "  double wv2 = wv*wv; ~%"),
    printf(fh, "  double dfac_v2 = dfac_v*dfac_v; ~%")
  ),
  printf(fh, "  double q2 = q_*q_; ~%"),
  printf(fh, "  double incr[~a]; ~%", length(bP)),
  flush_output(fh),

  /* expand background magnetic field Bmag, assuming it only varies in x */
  Bmag_expd:subst(Bignorevars,doExpand1(Bmag,bC)),
  BmagInv_expd:subst(Bignorevars,doExpand1(BmagInv,bC)),
  /* expand Phi */
  Phi_expd:doExpand1(Phi,bC),

  if emflag then (
    /* expand Apar */
    Apar_expd:doExpand1(Apar,bC),
    /* expand dApar/dt */
    dAdt_expd:doExpand1(dApardt,bC),
    dAdtProv_expd:doExpand1(dApardtProv,bC)
    /* NOTE: even though Apar and dApar/dt are allowed
       to be discontinuous in the parallel (z) direction,
       the surface term in the z direction does not involve
       Apar. since Apar is continuous in the other directions,
       it does not matter if we use the right or left value */
  )
  else (
    Apar_expd : 0,
    dAdt_expd : 0,
    dAdtProv_expd : 0
  ),

  /* calculate full expansion expression for Hamiltonian */
  H_full: q_*Phi_expd,
  if vdim > 0 then H_full : H_full + 1/2*m_*(wv + vpar/dfac_v)^2,
  if vdim > 1 then H_full : H_full + (wm+mu/dfac_m)*Bmag_expd,
  
  /* project Hamiltonian onto basis functions */
  H_k:fullratsimp(innerProd(varsP, 1, bP, H_full)),
  /* expand projected Hamiltonian on basis */
  H_expd:H_k.bP,

  /* expand distribution function on basis */
  fL_expd:doExpand1(fl,bP),
  fR_expd:doExpand1(fr,bP),

  /* expand BstarZ/Bmag = bhat.grad(z) = Gradpar on basis */
  BstarZ_by_Bmag_expd : subst(Bignorevars, doExpand1(Gradpar, bC)),

  /* expand BdriftX = 1/B*curl(bhat)*grad(x) on basis */
  BdriftX_expd : subst(Bignorevars, doExpand1(BdriftX, bC)),
  /* expand BdriftY = 1/B*curl(bhat)*grad(y) on basis */
  BdriftY_expd : subst(Bignorevars, doExpand1(BdriftY, bC)),
  /* expand BstarX/Bmag = ( m/q vpar + Apar ) BdriftX = (m/q vpar + Apar)/B*curl(bhat)*grad(x) on basis */
  BstarX_by_Bmag_expd : (m_/q_*(wv+vpar/dfac_v) + Apar_expd)*BdriftX_expd,
  /* expand BstarY/Bmag = ( m/q vpar + Apar ) BdriftY = (m/q vpar + Apar)/B*curl(bhat)*grad(y) on basis */
  BstarY_by_Bmag_expd : (m_/q_*(wv+vpar/dfac_v) + Apar_expd)*BdriftY_expd,
  /* make vApar = -1/B*bhatXgrad(Apar) vector */
  if cdim = 1 then vApar_expd : [0], 
  if cdim = 2 then vApar_expd : [diff(Apar_expd, y)*dfac_y, -diff(Apar_expd,x)*dfac_x]*BmagInv_expd,
  if cdim = 3 then vApar_expd : [diff(Apar_expd, y)*dfac_y, -diff(Apar_expd,x)*dfac_x, 0]*BmagInv_expd, 

  /* make Bstar/Bmag vector */
  if cdim = 1 then Bstar_by_Bmag_expd : [BstarZ_by_Bmag_expd],
  if cdim = 2 then Bstar_by_Bmag_expd : [BstarX_by_Bmag_expd, BstarY_by_Bmag_expd] + vApar_expd,
  if cdim = 3 then Bstar_by_Bmag_expd : [BstarX_by_Bmag_expd, BstarY_by_Bmag_expd, BstarZ_by_Bmag_expd] + vApar_expd,

  /* calculate phase space velocity alpha in direction of surfVar */
  /* we assume alpha.n is continuous across boundary, although H may not be */
  /* distinguish between alpha and alphaUp, where alphaUp is the one used to determine upwind direction */
  alphaUp_expd : gkPB(wSurf + surfVar/dfacSurf, H_expd) - q_/m_*dAdtProv_expd,
  alpha_expd : -q_/m_*dAdt_expd,
  /* calculate expression for surface-averaged alpha, and print to c variable alpha0 */
  alpha0expr : gcfac(fullratsimp(innerProd(surfIntVars, 1, subst(surfVar=-1,alphaUp_expd), subst(surfVar=-1, bP[1]))*bP[1])),
  printf(fh, "  // surface-averaged phase velocity in this direction ~%"),
  printf(fh, "  double alpha0 = ~a; ~%~%", float(subst([wv^2=wv2,q_^2=q2,dfac_v^2=dfac_v2], alpha0expr))),
  flush_output(fh),

  /* generate surface basis */
  bSurf : sqrt(2)*delete(0,fullratsimp(innerProd(varsP,1,bP,subst(surfVar=0,bP)))*bP),

  /* project full alpha expression evaluatated at interior surface onto surface basis and print to c variable alpha */
  printf(fh, "  double alpha[~a]; ~%", length(bSurf)),
  alpha_k : fullratsimp(innerProd(surfIntVars, 1, bSurf, subst(surfVar=-1,alpha_expd))),
  flush_output(fh),
  writeCExprsNoExpand1(alpha, subst([wv^2=wv2,dfac_v^2=dfac_v2, m_^2=m2, q_^2=q2],alpha_k)),
  alphaNoZero_k : doMakeExprLst(alpha_k, alpha),
  alphaSurf_expd : doExpandLst(alphaNoZero_k, bSurf),
  flush_output(fh),

  printf(fh, "  double alphaUp[~a]; ~%", length(bSurf)),
  alphaUp_k : fullratsimp(innerProd(surfIntVars, 1, bSurf, subst(surfVar=-1,alphaUp_expd))),
  flush_output(fh),
  writeCExprsNoExpand1(alphaUp, subst([wv^2=wv2,dfac_v^2=dfac_v2, m_^2=m2, q_^2=q2],alphaUp_k)),
  alphaUpNoZero_k : doMakeExprLst(alphaUp_k, alphaUp),
  alphaUpSurf_expd : doExpandLst(alphaUpNoZero_k, bSurf),
  flush_output(fh),

  /* note: always use pureUpwind for p=1 */
  if (alphaSurf_expd = 0) then (
    printf(fh, "  // alpha == 0, so nothing to do ~%"),
    printf(fh, "  return std::abs(alpha0); ~%"),
    printf(fh, "} ~%"),
    flush_output(fh)
  )
  else if pureUpwind or polyOrder=1 then (
    printf(fh, "#if upwindType == SURFAVG ~%"),
    printf(fh, "  if (alpha0>0) { ~%"),
    fhatSurf_expd : subst(surfVar=1, fL_expd),
    t : surfac*calcInnerProdList(surfIntVars, alphaSurf_expd, subst(surfVar=-1, bP), fhatSurf_expd),
    incr_r : facsum(fullratsimp(t),dxv),
    writeCExprsNoExpand1(incr, subst([wv^2=wv2,q_^2=q2], incr_r)),
    printf(fh, "  } else { ~%"),
    fhatSurf_expd : subst(surfVar=-1, fR_expd),
    t : surfac*calcInnerProdList(surfIntVars, alphaSurf_expd, subst(surfVar=-1, bP), fhatSurf_expd),
    incr_r : facsum(fullratsimp(t),dxv),
    writeCExprsNoExpand1(incr, subst([wv^2=wv2,q_^2=q2], incr_r)),
    printf(fh, "  }~%"),
    flush_output(fh),

    printf(fh, "#elif upwindType == QUAD ~%"),
    printf(fh, "double fupwind[~a];~%", length(bSurf)),
    printf(fh, "double fupwindQuad[~a];~%", length(bSurf)),
    printf(fh, "double alphaQuad;~%"),
    if polyOrder=1 then quad:1/sqrt(3),
    if polyOrder=2 then quad:sqrt(3/5),
    nodes : eval_string(sconcat("nodes", cdim+vdim-1, "xp", polyOrder))*quad,
    /* evaluate fl, fr, and alpha at surface quadrature nodes */
    frQ:gcfac(float(nodesub(subst(surfVar=-1,fR_expd),surfIntVars,nodes))),
    flQ:gcfac(float(nodesub(subst(surfVar=1,fL_expd),surfIntVars,nodes))), 
    /* note: alphaSurf_expd is already a surface expansion */
    alphaq:gcfac(float(nodesub(alphaUpSurf_expd,surfIntVars,nodes))),
    /* determine upwinding at each surface quadrature node */
    fupQ : gcfac(.5*(flQ+frQ)+.5*sgn(alphaQuad)*(flQ-frQ)),
    for i:1 thru length(alphaq) do (
    printf(fh, "  alphaQuad = ~a; ~%", alphaq[i]),
    printf(fh, "  fupwindQuad[~a] = ~a; ~%", i-1, fupQ[i])
    ),
    flush_output(fh),
    /* use modToNod transformation to get modal surface expansion from nodal quadrature points */
    modToNod:fullratsimp(calcModToNodPhaseWithNodesAndBasis(nodes,bSurf,surfIntVars)),
    fupwind_k:invert(transpose((modToNod))).makelist(fupwindQuad[i-1],i,1,length(nodes)),
    fupwind_k:gcfac(makelist(fupwind_k[i][1],i,1,length(fupwind_k))),
    /* write coefficients of modal surface expansion fupwind */
    writeCExprsNoExpand1(fupwind, fupwind_k),
    fhatSurf_expd:doExpand1(fupwind,bSurf),
    flush_output(fh),

    t : surfac*calcInnerProdList(surfIntVars, alphaSurf_expd, subst(surfVar=-1, bP), fhatSurf_expd),
    incr_r : facsum(fullratsimp(t),dxv),
    writeCExprsNoExpand1(incr, subst([wv^2=wv2,q_^2=q2], incr_r)),
    printf(fh, "~%"),
    flush_output(fh),
    printf(fh, "#endif ~%"),

    incr_s : makelist(incr[i-1], i, 1, length(bP)),
    writeCIncrExprsNoExpand1(outr, incr_s),  
    printf(fh, "~%"),
    flush_output(fh),
    t : surfac*calcInnerProdList(surfIntVars, alphaSurf_expd, subst(surfVar=1, bP), fhatSurf_expd),
    incr_l : -t,
    signs : fullratsimp(incr_l/incr_r),
    incr_s : makelist(signs[i]*incr[i-1], i, 1, length(bP)),
    writeCIncrExprsNoExpand1(outl, incr_s),
    flush_output(fh),
    printf(fh, "~%"),
    
    printf(fh, "  return std::abs(alpha0); ~%"),
    printf(fh, "} ~%"),
    flush_output(fh)
    
  ) else (
    /* determine whether to use local or global value for penalty amax */
    printf(fh, "  double amax = 0.0; ~%"),
    printf(fh, "  bool upwind = false; ~%"), 
    printf(fh, "  if(upwind) ~%"),
    printf(fh, "    amax = std::abs(alpha0); ~%"),
    printf(fh, "  else ~%"),
    printf(fh, "    amax = amax_in; ~%~%"),
    printf(fh, "  double Ghat[~a]; ~%", length(bSurf)),
    printf(fh, "  double favg[~a]; ~%", length(bSurf)),

    /* print C exprs for favg = fr(sv=-1) + fl(sv=1) projected onto surface basis */
    favg_k : gcfac(fullratsimp(innerProd(surfIntVars, 1, bSurf, subst(surfVar=-1, fR_expd)+subst(surfVar=1, fL_expd)))),
    writeCExprsNoExpand1(favg, favg_k),
    favgNoZero_k : doMakeExprLst(favg_k, favg),
    favg_expd : doExpandLst(favgNoZero_k, bSurf),
    flush_output(fh),

    /* project fjump = fr(sv=-1) - fl(sv=1) onto surface basis. don't need to print it out because it is just part of Ghat */
    fjump_k : gcfac(fullratsimp(innerProd(surfIntVars, 1, bSurf, subst(surfVar=-1, fR_expd)-subst(surfVar=1, fL_expd)))),
    printf(fh, "~%"),
    flush_output(fh),

    /* evaluate surface projection of Ghat = (alpha*favg - amax*fjump)/2 */
    t_k : gcfac(innerProd(surfIntVars, favg_expd, bSurf, alphaSurf_expd)),
    printf(fh, "//~%"),
    flush_output(fh),
    Ghat_k : fullratsimp(t_k/2 - amax/2*fjump_k),
    printf(fh, "// //~%"),
    flush_output(fh),
    writeCExprsNoExpand1(Ghat, Ghat_k),
    flush_output(fh),
    Ghat_expd : doExpand1(Ghat, bSurf),

    /* evaluate contribution to right boundary (surfVar=-1) */
    incr_r : gcfac(fullratsimp(surfac*innerProd(surfIntVars, 1, subst(surfVar=-1, bP), Ghat_expd))),
    writeCExprsNoExpand1(incr, incr_r),
    printf(fh, "~%"),
    incr_s : makelist(incr[i-1], i, 1, length(bP)),
    writeCIncrExprsNoExpand1(outr, incr_s),  
    printf(fh, "~%"),
    flush_output(fh),
    /* contribution to left boundary (surfVar=1) is same except for signs */
    incr_l : gcfac(fullratsimp(surfac*innerProd(surfIntVars, -1, subst(surfVar=1, bP), Ghat_expd))),
    signs : fullratsimp(incr_l/incr_r),
    incr_s : makelist(signs[i]*incr[i-1], i, 1, length(bP)),
    writeCIncrExprsNoExpand1(outl, incr_s),

    /* return alpha0 so that we can find global value */
    printf(fh, "return std::abs(alpha0); ~%"),
    
    printf(fh, "} ~%"),
    flush_output(fh)
  )
)$

calcGKSurfUpdater(fh, funcNm, cdim, vdim, basisFun,p,emflag, bvarslist) := block([],
  if vdim = 0 then vd : 0
  else vd : 1,
  for dir : 1 thru cdim+vd do (
    /* always pure upwind in z direction */
    if (dir=cdim and cdim#2) then (
      pureUpwind : true
    ) else (
      pureUpwind : false
    ),
    calcUpdateInDir(dir, fh, funcNm, cdim, vdim, basisFun,p,emflag, bvarslist, pureUpwind),
    if (dir = cdim+1 and p=1 and emflag=true and positivity=false) then calcStep2Update(fh, funcNm, cdim, vdim, basisFun,p,emflag, bvarslist, pureUpwind) 
  )
)$

pmax:1$
em:false$

for pos: 1 thru 1 do (
if pos=0 then positivity:false
else positivity:true,
if positivity then posString : "Positivity"
else posString : "",

for c : 1 thru 3 do (
for v: 1 thru 2 do (
if c=1 or (c>1 and v=2) then (
if positivity then pmax:1,
for p:1 thru pmax do (
/* separate file for positivity implementation */
if em then fname : sconcat("~/max-out/EmGyrokineticSurf", posString, "Ser", c, "x", v, "vP", p, ".cpp")
else fname : sconcat("~/max-out/GyrokineticSurf", posString, "Ser", c, "x", v, "vP", p, ".cpp"),
fh : openw(fname),
printf(fh, "#include <GyrokineticModDecl.h> ~%"),
if p>1 then printf(fh, "#define upwindType SURFAVG~%"),
/* possible combinations of variable dependence of background magnetic field,
   with 0 = const, 1 = x, 2 = y, 3 = z.
   note that we assume axisymmetry, which means B cannot depend on y */
if c=1 then bvarslist : [[0], [1]],
if c=2 then bvarslist : [[0], [1]],
if c=3 then bvarslist : [[0], [1]/*, [3], [1,3]*/],
for b : 1 thru length(bvarslist) do (
if em then (
funcName : sconcat("EmGyrokineticSurf", posString, c, "x", v, "v", "Ser"),
calcGKSurfUpdater(fh, funcName, c, v, "Ser", p, true, bvarslist[b])
) else (
funcName : sconcat("GyrokineticSurf", posString, c, "x", v, "v", "Ser"),
calcGKSurfUpdater(fh, funcName, c, v, "Ser", p, false, bvarslist[b])
)),
close(fh)
)))),

if em=false then (
c:2,
v:0,
pmax:1,
if positivity then pmax:1,
for p:1 thru pmax do (
fname : sconcat("~/max-out/GyrokineticSurf", posString, "Ser", c, "x", v, "vP", p, ".cpp"),
fh : openw(fname),
printf(fh, "#include <GyrokineticModDecl.h> ~%"),
bvarslist : [[0]],
funcName : sconcat("GyrokineticSurf", posString, c, "x", v, "v", "Ser"),
calcGKSurfUpdater(fh, funcName, c, v, "Ser", p, false, [0]),
close(fh)
)
))$
