load("modal-basis")$
load("basis-precalc/basisSer1x")$
assume(dx>0)$

eta(xc,dx,b) := subst(z=x,
  subst(x=(z-xc)/(dx/2), b))$

/* Reconstruction across two cells of size dx each */
bCC : basisC[2]$

pL : eta(-dx/2,dx,bCC)$
pR : eta(dx/2,dx,bCC)$

N : length(bCC)$
h : doExpand1(hn, makelist(x^i,i,0,2*N-1))$
eqL : calcInnerProdListGen([x], [[-dx,0]], 1, pL, h-doExpand(hL,pL))$
eqR : calcInnerProdListGen([x], [[0,dx]],  1, pR, h-doExpand(hR,pR))$
eqList : append(eqL,eqR)$
hsol : linsolve(eqList, makelist(hn[i], i, 0, 2*N-1))$
h1d : fullratsimp(subst(hsol, h))$

/* Three-cell reconstruction: match slope and moments */
bC : eta(0, dx, bCC)$
fh : sum(f(j,i)*bC[i], i, 1, N)$

dh : subst(x=0,diff(h1d,x))$

/* Slope on right cell edge */
subListR : append(
  makelist(hL[i]=f(j,i), i, 1,N), makelist(hR[i]=f(j+1,i), i, 1,N)
  )$
dhR : subst(subListR, dh)$

/* Slope on left cell edge */
subListL : append(
  makelist(hL[i]=f(j-1,i), i, 1,N), makelist(hR[i]=f(j,i), i, 1,N)
  )$
dhL : subst(subListL, dh)$

/* Reconstruct in the middle cell */
rh : sum(r[i]*x^i, i, 0, N+1)$

/* Match slopes */
eq1 : dhR = subst(x=dx/2,diff(rh,x))$
eq2 : dhL = subst(x=-dx/2,diff(rh,x))$
/* Match moments */
eqN : calcInnerProdListGen([x], [[-dx/2,dx/2]], 1, bC, rh-fh)$
eqList : append([eq1, eq2], eqN)$

/* Solve system and construct expansion */
rsol : linsolve(eqList, makelist(r[i],i,0,N+1))$
r1d : subst(rsol, rh)$

/* Basis function in three cells: L,I,R */
bCL : eta(-dx,dx,bCC)$
bCI : eta(0,dx,bCC)$
bCR : eta(dx,dx,bCC)$

/* Function to compute reconstruction of an expression */
calcRecon(g) := block([gL, gI, gR, r1g],
  gL : calcInnerProdListGen([x], [[-3*dx/2,-dx/2]], 1, bCL, g)*2/dx,
  gI : calcInnerProdListGen([x], [[-dx/2,dx/2]], 1, bCI, g)*2/dx,
  gR : calcInnerProdListGen([x], [[dx/2,3*dx/2]], 1, bCR, g)*2/dx,
  r1g : expand(
    fullratsimp(subst(append(
      makelist(f(j-1,i)=gL[i],i,1,N),
      makelist(f(j,i)=gI[i],i,1,N),
      makelist(f(j+1,i)=gR[i],i,1,N)),
    r1d))),
  return (r1g)
  )$

/* Taylor series analysis */
taylorExpand(df,N,z) := df[0] + sum(df[i]*z^i/factorial(i), i, 1, N)$
ft : taylorExpand(df, 10, x)$
ft1d : calcRecon(ft)$

/* Project Taylor series on basis functions */
ft1d_xx_h : expand(calcInnerProdListGen([x], [[-dx/2,dx/2]], 1, bC, diff(ft1d,x,2))*2/dx)$

/* dsin is a "discrete" sin */
dsin(x,n) := sum((-1)^(i-1)*x^(2*i-1)/factorial(2*i-1), i, 1, n)$

/* Calculate errors in reconstruction in a cell */
calcError(g, dxIn) := sqrt(
  integrate((subst(dx=dxIn,g-calcRecon(g)))^2, x, -dxIn/2, dxIn/2)/dxIn
  )$

/* Calculate errors in second derivatives */
calcD2Error(g, dxIn) := sqrt(
  integrate((subst(dx=dxIn,diff(g-calcRecon(g),x,2)))^2, x, -dxIn/2, dxIn/2)/dxIn
  )$

/* Calculate order of convergence */
calcOrder(dx, errorDx) := float(makelist(
  log(errorDx[i]/errorDx[i+1])/log(dx[i]/dx[i+1]),
  i, 1, length(dx)-1))$

g : dsin(x-1/2,10) + dsin(2*x,10)$

/* Make list of errors */
dxVal : [2, 1, 1/2, 1/4, 1/8]$
reconErr : map(lambda([d], calcError(g,d)), dxVal)$
reconConvOrder : calcOrder(dxVal, reconErr)$

diff2Err : map(lambda([d], calcD2Error(g,d)), dxVal)$
diff2ConvOrder : calcOrder(dxVal, diff2Err)$

/* Function to plot reconstruction */
plotRecon(g,dx) := block([gL,gI,gR,ge,r1ge,gLe,gIe,gRe],
  gL : calcInnerProdListGen([x], [[-3,-1]*dx/2], 1, bCL, g)*2/dx,
  gI : calcInnerProdListGen([x], [[-1,1]*dx/2], 1, bCI, g)*2/dx,
  gR : calcInnerProdListGen([x], [[1,3]*dx/2], 1, bCR, g)*2/dx,
  ge : explicit(g, x, -3*dx/2, 3*dx/2),
  r1ge : explicit(calcRecon(g), x, -dx/2, dx/2),
  gLe : explicit(gL.bCL, x, -3*dx/2, -dx/2),
  gIe : explicit(gI.bCI, x, -dx/2, dx/2),
  gRe : explicit(gR.bCR, x, dx/2, 3*dx/2),
  draw2d(
    grid=true,
    color=red, r1ge,
    color=black,gLe,
    color=black,gIe,
    color=black,gRe,
    color=blue,line_type=dots,ge
    )
  )$
plotRecon(g, 2)$
