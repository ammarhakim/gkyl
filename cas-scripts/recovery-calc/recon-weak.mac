load("modal-basis")$
load("basis-precalc/basisSer1x")$

bC : basisC[1]$
eta(xc,dx,b) := subst(x=(z-xc)/(dx/2), b)$
pL : eta(-1,2,bC)$
pR : eta(1,2,bC)$

N : length(bC)$
h : doExpand1(hn, makelist(z^i,i,0,2*N-1))$
eqL : calcInnerProdListGen([z], [[-2,0]], 1, pL, h-doExpand(hL,pL))$
eqR : calcInnerProdListGen([z], [[0,2]],  1, pR, h-doExpand(hR,pR))$
eqList : append(eqL,eqR)$
hsol : linsolve(eqList, makelist(hn[i], i, 0, 2*N-1))$
h1d : subst(hsol, h)$

fh : sum(f(j,i)*bC[i], i, 1, N)$
dh : subst(z=0,diff(h1d,z))$

subListR : append(
  makelist(hL[i]=f(j,i), i, 1,N), makelist(hR[i]=f(j+1,i), i, 1,N)
  )$
dhR : subst(subListR, dh)$

subListL : append(
  makelist(hL[i]=f(j-1,i), i, 1,N), makelist(hR[i]=f(j,i), i, 1,N)
  )$
dhL : subst(subListL, dh)$

rh : sum(r[i]*x^i, i, 0, N+1)$

eq1 : dhR = subst(x=1,diff(rh,x))$
eq2 : dhL = subst(x=-1,diff(rh,x))$
eqN : calcInnerProdListGen([x], [[-1,1]], 1, bC, rh-fh)$
eqList : append([eq1, eq2], eqN)$

rsol : linsolve(eqList, makelist(r[i],i,0,N+1))$
r1d : subst(rsol, rh)$

r1dxx : diff(r1d,x,2)$

rxxExp : calcInnerProdList([x], 1, bC, diff(r1d,x,2))$

/* Taylor series analysis */
taylorExpand(df,N,z) := df[0] + sum(df[i]*z^i/factorial(i), i, 1, N)$

ft : taylorExpand(df, 4, z)$

bCL : eta(-2,2,bC)$
bCI : eta(0,2,bC)$
bCR : eta(2,2,bC)$

ftL : calcInnerProdListGen([z], [[-3,-1]], 1, bCL, ft)$
ftI : calcInnerProdListGen([z], [[-1,1]], 1, bCI, ft)$
ftR : calcInnerProdListGen([z], [[1,3]], 1, bCR, ft)$

subList : append(
  makelist(f(j-1,i)=ftL[i],i,1,N),
  makelist(f(j,i)=ftI[i],i,1,N),
  makelist(f(j+1,i)=ftR[i],i,1,N))$

ft1d : subst(subList, r1d)$

/* For testing */

g : (z-1/2)^2$
gL : calcInnerProdListGen([z], [[-3,-1]], 1, bCL, g)$
gI : calcInnerProdListGen([z], [[-1,1]], 1, bCI, g)$
gR : calcInnerProdListGen([z], [[1,3]], 1, bCR, g)$

subList : append(
  makelist(f(j-1,i)=gL[i],i,1,N),
  makelist(f(j,i)=gI[i],i,1,N),
  makelist(f(j+1,i)=gR[i],i,1,N))$

r1g : subst(subList, r1d)$

/* Plot stuff */
ge : explicit(g, z, -3, 3)$
r1ge : explicit(r1g, x, -1, 1)$

gLe : explicit(gL.bCL, z, -3, -1)$
gIe : explicit(gI.bCI, z, -1, 1)$
gRe : explicit(gR.bCR, z, 1, 3)$

draw2d(
  grid=true,
  color=blue, ge,
  color=black, line_type=dots,gLe,
  color=black, line_type=dots,gIe,
  color=black, line_type=dots,gRe,
  color=red, r1ge
  )$


