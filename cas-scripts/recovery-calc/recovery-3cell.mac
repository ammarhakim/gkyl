calcRecov3CellGen(basisNm, recDir, dirs, polyOrder, lo, ce, up) := block(
    /* Returns recovered polynomial defined over 3 cells with a 3-cell stencil
  Inputs:
    basisNm : name of used basis function ("Ser", "Tensor", or "Max");
      NOTE: This makes no difference for 1D recoveries so it should
      ideally be an optional input but I don't see a way around it
      now. --Petr Cagas
    recDir : direction of the recovery (x, y, or z)
    dirs : direction names, e.g. [x, y]
    polyOrder : polynomial order of the inputs
    lo : can be one of the following
      * dg(A) : A is an array of expansion coefficients corresponding
        to logical space basis functions (in the lower cell)
      * bc(D=_, N=_, val=_) : a boundary condition definition
        D*f(x)+N*df(x)/dx=val which is used instead of expansion
        coefficients
      * dg(A, bc(D=_, N=_, val=_)) : a constrained recovery where
        expansion coefficients are used together with boundary
        conditions at the edge
    ce : dg(A), where A is an array of expansion coefficients corresponding
      to logical space basis functions (in the middle cell)
    up : similar to 'lo' but in the upper cell
  Outputs:
    recovered polynomial
  */
  [
  DoF, r, rExp, rSol, rSub,
  recEqList, projSubList,
  baLo1D, baCe1D, baUp1D, baLoND, baCeND, baUpND, baVars,
  dimProjLo, dimProjCe, dimProjUp, qLo1D, qCe1D, qUp1D,
  wx, wy, wz,
  numDims
  ],
  
  baCe1D : getBasis(basisNm, 1, polyOrder),
  baCe1D : subst(x=recDir, baCe1D),
  baLo1D : etaDir(recDir, -2, 2, baCe1D),
  baUp1D : etaDir(recDir, 2, 2, baCe1D),
  
  /* Forming and solving the equation system */
  DoF : polyOrder + 1, /* Number of degrees of freedom */
    if is(op(lo)=dg) then (
    DoF : DoF + polyOrder + 1,
    if length(args(lo)) > 1 then DoF : DoF + 1 /* aditional DoF for BC */
  )
  else (
    DoF : DoF + 1 /* only 1 DoF for BC */
  ),
  if is(op(up)=dg) then (
    DoF : DoF + polyOrder + 1,
    if length(args(up)) > 1 then DoF : DoF + 1 /* aditional DoF for BC */
  )
  else (
    DoF : DoF + 1 /* only 1 DoF for BC */
  ),
  rExp : doExpand(r, makelist(recDir^i, i, 0, DoF-1)), /* Recovery polynomial */

  recEqList : [], /* equation set from the weak equality formulation */
  if is(op(lo)=dg) then (
    recEqList : append(
      recEqList,
      calcInnerProdListGen([recDir], [[-3, -1]], 1,
        baLo1D, rExp-doExpand(qLo1D, baLo1D))
    ),
    if length(args(lo)) > 1 then (
      recEqList : append(
        recEqList,
        [subst(args(args(lo)[2]), val - D*subst(recDir=-3, rExp)
          - N*subst(recDir=-3, diff(rExp, recDir)))]
      )
    )
  )
  else (
    recEqList : append(
      recEqList,
      [subst(args(lo), val - D*subst(recDir=-1, rExp)
        - N*subst(recDir=-1, diff(rExp, recDir)))]
    )
  ),

  recEqList : append(
    recEqList,
    calcInnerProdListGen([recDir], [[-1, 1]], 1,
      baCe1D, rExp-doExpand(qCe1D, baCe1D))
  ),

  if is(op(up)=dg) then (
    recEqList : append(
      recEqList,
      calcInnerProdListGen([recDir], [[1, 3]], 1,
        baUp1D, rExp-doExpand(qUp1D, baUp1D))
    ),
    if length(args(up)) > 1 then (
      recEqList : append(
        recEqList,
        [subst(args(args(up)[2]), val - D*subst(recDir=3, rExp)
          - N*subst(recDir=3, diff(rExp, recDir)))]
      )
    )
  )
  else (
    recEqList : append(
      recEqList,
      [subst(args(up), val - D*subst(recDir=1, rExp)
        - N*subst(recDir=1, diff(rExp, recDir)))]
    )
  ),

  rSol : linsolve(recEqList, makelist(r[i], i, 1, DoF)),
  rSub : fullratsimp(subst(rSol, rExp)),

  /* backsubstitute the non-recovered directtions if needed */
  numDims : length(dirs),
  if numDims > 1 then (
    baCeND : getBasis(basisNm, numDims, polyOrder),
    baVars : listofvars(baCeND),
    baCeND : psubst(makelist(baVars[i]=dirs[i],i,1,numDims), baCeND),
    baLoND : etaDir(recDir, -2, 2, baCeND),
    baUpND : etaDir(recDir, 2, 2, baCeND),

    projSubList : [],
    if is(op(lo)=dg) then (
      dimProjLo : calcInnerProdListGen([recDir], [[-3, -1]],
        1, baLo1D, doExpand(args(lo)[1], baLoND)),
      projSubList : append (
        projSubList,
        makelist(qLo1D[i]=dimProjLo[i], i, 1, length(baCe1D))
      )
    ),
    dimProjCe : calcInnerProdListGen([recDir], [[-1, 1]],
      1, baCe1D, doExpand(args(ce)[1], baCeND)),
    projSubList : append (
      projSubList,
      makelist(qCe1D[i]=dimProjCe[i], i, 1, length(baCe1D))
    ),
    if is(op(up)=dg) then (
      dimProjUp : calcInnerProdListGen([recDir], [[1, 3]],
        1, baUp1D, doExpand(args(up)[1], baUpND)),
      projSubList : append (
        projSubList,
        makelist(qUp1D[i]=dimProjUp[i], i, 1, length(baCe1D))
      )
    )
  )
  else (
    projSubList : [],
    if is(op(lo)=dg) then (
      projSubList : append (
        projSubList,
        makelist(qLo1D[i]=args(lo)[1][i], i, 1, length(baCe1D))
      )
    ),
    projSubList : append (
      projSubList,
      makelist(qCe1D[i]=args(ce)[1][i], i, 1, length(baCe1D))
    ),
    if is(op(up)=dg) then (
      projSubList : append (
        projSubList,
        makelist(qUp1D[i]=args(up)[1][i], i, 1, length(baCe1D))
      )
    )
  ),

  return(expand(subst(projSubList, rSub)))
) $
