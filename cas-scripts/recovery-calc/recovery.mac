load("modal-basis") $
assume(dv[0]>0, dv[1]>0, dv[2]>0) $

etaDir(dir, xc, dx, basis) := subst(td=dir, subst(dir=2*(td-xc)/dx, basis)) $

eta(xc, dx, basis) := block([out, tx, ty, tz],
  out : subst(tx=x, subst(x=2*(tx-xc[1])/dx[1], basis)),
  if length(dx) > 1 then ( out : subst(ty=y, subst(y=2*(ty-xc[2])/dx[2], out)) ),
  if length(dx) > 2 then ( out : subst(tz=z, subst(z=2*(tz-xc[3])/dx[3], out)) ),
  return(out)
  ) $

getDirIdx(dir, numDims) := block([],
  if numDims = 1 then return(1),
  if dir = x then return(1),
  if dir = y then return(2),
  if dir = z then return(3)
  ) $

getRecov2CellGen(dir, dx, polyOrder, qM, qP) := block(
  [N, basis1D, basisND, numDims, dIdx,
  baM1D, baMND, baP1D, baPND, 
  r, rExp, rSol, rSub, recEqList, qM1D, qP1D],
  
  numDims : length(dx),
  dIdx : getDirIdx(dir, numDims),
  load("basis-precalc/basisSer1x"),
  basis1D : subst(x=dir, basisC[polyOrder]),
  baM1D : etaDir(dir, -dx[dIdx]/2, dx[dIdx], basis1D),
  baP1D : etaDir(dir, dx[dIdx]/2, dx[dIdx], basis1D),
  
  /* Forming and solving the equation system */
  N : polyOrder + 1,
  rExp : doExpand(r, makelist(dir^i, i, 0, 2*N-1)),
  recEqList : append(
    calcInnerProdListGen([dir], [[-dx[dIdx], 0]], 1, baM1D, rExp-doExpand(qM1D, baM1D)),
    calcInnerProdListGen([dir], [[0, dx[dIdx]]], 1, baP1D, rExp-doExpand(qP1D, baP1D))
    ),
  rSol : linsolve(recEqList, makelist(r[i], i, 1, 2*N)),
  rSub : fullratsimp(subst(rSol, rExp)),

  if numDims > 1 then (
    /* Creating substitution lists to get the y-dependence */
    load(sconcat("basis-precalc/basisSer", numDims, "x")),
    basisND : basisC[polyOrder],
    xc : [0, 0, 0],
    xc[dIdx] : -dx[dIdx]/2,
    baMND : eta(xc, dx, basisND),
    xc[dIdx] : dx[dIdx]/2,
    baPND : eta(xc, dx, basisND),
    dimProjM : calcInnerProdListGen([dir], [[-dx[dIdx], 0]], 1, baM1D, doExpand(qM, baMND)),
    dimProjP : calcInnerProdListGen([dir], [[0, dx[dIdx]]], 1, baP1D, doExpand(qP, baPND)),
    projSubList : append (
      makelist(qM1D[i]=dimProjM[i], i, 1, length(basis1D)),
      makelist(qP1D[i]=dimProjP[i], i, 1, length(basis1D))
      )
    )
  else (
    projSubList : append (
      makelist(qM1D[i]=qM[i], i, 1, length(basis1D)),
      makelist(qP1D[i]=qP[i], i, 1, length(basis1D))
      )
    ),

  return(subst(projSubList, rSub))
  ) $

getRecov3CellGen(dir, dx, polyOrder, C, qM, qC, qP) := block(
  [N, basis1D, basisND, numDims,
  baM1D, baMND, baC1D, baCND, baP1D, baPND,
  r, rExp, rSol, rSub, recEqList, qM1D, qC1D, qP1D,
  rM, rP],
  
  numDims : length(dx),
  dIdx : getDirIdx(dir, numDims),
  load("basis-precalc/basisSer1x"),
  basis1D : subst(x=dir, basisC[polyOrder]),
  baC1D : etaDir(dir, 0, dx[dIdx], basis1D),

  N : polyOrder + 3,
  if C = 1 then N : N + 2,
  /* Forming and solving the equation system */
  rExp : doExpand(r, makelist(dir^i, i, 0, N-1)),
  rM : getRecov2CellGen(dir, [dx[dIdx]], polyOrder, qM1D, qC1D),
  rP : getRecov2CellGen(dir, [dx[dIdx]], polyOrder, qC1D, qP1D),
  recEqList : append(
    [subst(dir=0, rM) - subst(dir=-dx[dIdx]/2, rExp)],
    [subst(dir=0, rP) - subst(dir=dx[dIdx]/2, rExp)],
    calcInnerProdListGen([dir], [[-dx[dIdx]/2, dx[dIdx]/2]], 1, baC1D, rExp-doExpand(qC1D, baC1D))
    ),
  if C = 1 then (
    recEqList : append(
      recEqList,
      [subst(dir=0, diff(rM, dir)) - subst(dir=-dx[dIdx]/2, diff(rExp, dir))],
      [subst(dir=0, diff(rP, dir)) - subst(dir=dx[dIdx]/2, diff(rExp, dir))]
      )
    ),
  rSol : linsolve(recEqList, makelist(r[i], i, 1, N)),
  rSub : fullratsimp(subst(rSol, rExp)),
  
  if numDims > 1 then (
    /* Creating substitution lists to get the y-dependence */
    baM1D : etaDir(dir, -dx[dIdx], dx[dIdx], basis1D),
    baP1D : etaDir(dir, dx[dIdx], dx[dIdx], basis1D),
    load(sconcat("basis-precalc/basisSer", numDims, "x")),
    basisND : basisC[polyOrder],
    xc : [0, 0, 0],
    xc[dIdx] : -dx[dIdx],
    baMND : eta(xc, dx, basisND),
    xc[dIdx] : 0,
    baCND : eta(xc, dx, basisND),
    xc[dIdx] : dx[dIdx],
    baPND : eta(xc, dx, basisND),
    
    dimProjM : calcInnerProdListGen([dir], [[-3*dx[dIdx]/2, -dx[dIdx]/2]], 1, baM1D, doExpand(qM, baMND)),
    dimProjC : calcInnerProdListGen([dir], [[-dx[dIdx]/2, dx[dIdx]/2]], 1, baC1D, doExpand(qC, baCND)),
    dimProjP : calcInnerProdListGen([dir], [[dx[dIdx]/2, 3*dx[dIdx]/2]], 1, baP1D, doExpand(qP, baPND)),
    projSubList : append (
      makelist(qM1D[i]=dimProjM[i], i, 1, length(basis1D)),
      makelist(qC1D[i]=dimProjC[i], i, 1, length(basis1D)),
      makelist(qP1D[i]=dimProjP[i], i, 1, length(basis1D))
      )
    )
  else (
    projSubList : append (
      makelist(qM1D[i]=qM[i], i, 1, length(basis1D)),
      makelist(qC1D[i]=qC[i], i, 1, length(basis1D)),
      makelist(qP1D[i]=qP[i], i, 1, length(basis1D))
      )
    ),

  return(subst(projSubList, rSub))
  ) $


getRecov4CellGen(dirs, dx, polyOrder, qTL, qTR, qBR, qBL) := block(
  [N, basis1D, basisND, numDims, dIdx,
  baM1D, baMND, baP1D, baPND, 
  r, rExp, rSol, rSub, recEqList, qM1D, qP1D],
  
  numDims : length(dx),
  dIdx1 : getDirIdx(dirs[1], numDims),
  dIdx2 : getDirIdx(dirs[2], numDims),
  load("basis-precalc/basisSer2x"),
  basis2D : subst([x=dirs[1], y=dirs[2]], basisC[polyOrder]),
  xc : [0, 0, 0],
  xc[dIdx1] : -dx[dIdx1]/2,
  xc[dIdx2] : dx[dIdx2]/2,
  baTL2D : eta(xc, dx, basis2D),
  xc[dIdx1] : dx[dIdx1]/2,
  xc[dIdx2] : dx[dIdx2]/2,
  baTR2D : eta(xc, dx, basis2D),
  xc[dIdx1] : dx[dIdx1]/2,
  xc[dIdx2] : -dx[dIdx2]/2,
  baBR2D : eta(xc, dx, basis2D),
  xc[dIdx1] : -dx[dIdx1]/2,
  xc[dIdx2] : -dx[dIdx2]/2,
  baBL2D : eta(xc, dx, basis2D),
  
  /* Forming and solving the equation system */
  N : polyOrder + 1,
  rExp : doExpand(r, create_list(dirs[1]^i*dirs[2]^j, i, 0, 2*N-1, j, 0, 2*N-1)),
  recEqList : append(
    calcInnerProdListGen(dirs, [[-dx[dIdx1], 0], [0, dx[dIdx2]]], 1, baTL2D, rExp-doExpand(qTL2D, baTL2D)),
    calcInnerProdListGen(dirs, [[0, dx[dIdx1]], [0, dx[dIdx2]]], 1, baTR2D, rExp-doExpand(qTR2D, baTR2D)),
    calcInnerProdListGen(dirs, [[0, dx[dIdx1]], [-dx[dIdx2], 0]], 1, baBR2D, rExp-doExpand(qBR2D, baBR2D)),
    calcInnerProdListGen(dirs, [[-dx[dIdx1], 0], [-dx[dIdx2], 0]], 1, baBL2D, rExp-doExpand(qBL2D, baBL2D))
    ),
  rSol : linsolve(recEqList, makelist(r[i], i, 1, 4*N*N)),
  rSub : fullratsimp(subst(rSol, rExp)),

  if numDims > 2 then (
    /* Creating substitution lists to get the y-dependence */
    load(sconcat("basis-precalc/basisSer", numDims, "x")),
    basisND : basisC[polyOrder],
    xc : [0, 0, 0],
    xc[dIdx1] : -dx[dIdx1]/2,
    xc[dIdx2] : dx[dIdx2]/2,
    baTLND : eta(xc, dx, basisND),
    xc[dIdx1] : dx[dIdx1]/2,
    xc[dIdx2] : dx[dIdx2]/2,
    baTRND : eta(xc, dx, basisND),
    xc[dIdx1] : dx[dIdx1]/2,
    xc[dIdx2] : -dx[dIdx2]/2,
    baBRND : eta(xc, dx, basisND),
    xc[dIdx1] : -dx[dIdx1]/2,
    xc[dIdx2] : -dx[dIdx2]/2,
    baBLND : eta(xc, dx, basisND),
    
    dimProjTL : calcInnerProdListGen(dirs, [[-dx[dIdx1], 0], [0, dx[dIdx2]]], 1, baTL2D, doExpand(qTL, baTLND)),
    dimProjTR : calcInnerProdListGen(dirs, [[0, dx[dIdx1]], [0, dx[dIdx2]]], 1, baTR2D, doExpand(qTR, baTRND)),
    dimProjBR : calcInnerProdListGen(dirs, [[0, dx[dIdx1]], [-dx[dIdx2], 0]], 1, baBR2D, doExpand(qBR, baBRND)),
    dimProjTL : calcInnerProdListGen(dirs, [[-dx[dIdx1], 0], [-dx[dIdx2], 0]], 1, baBL2D, doExpand(qBL, baBLND)),
    projSubList : append (
      makelist(qTL2D[i]=dimProjTL[i], i, 1, length(basis2D)),
      makelist(qTR2D[i]=dimProjTR[i], i, 1, length(basis2D)),
      makelist(qBR2D[i]=dimProjBR[i], i, 1, length(basis2D)),
      makelist(qBL2D[i]=dimProjBL[i], i, 1, length(basis2D))
      )
    )
  else (
    projSubList : append (
      makelist(qTL2D[i]=qTL[i], i, 1, length(basis2D)),
      makelist(qTR2D[i]=qTR[i], i, 1, length(basis2D)),
      makelist(qBR2D[i]=qBR[i], i, 1, length(basis2D)),
      makelist(qBL2D[i]=qBL[i], i, 1, length(basis2D))
      )
    ),

  return(subst(projSubList, rSub))
  ) $


getRecov6CellGen(dirs, dx, polyOrder, C, qTL, qTC, qTR, qBR, qBC, qBL) := block(
  [N, basis1D, basisND, numDims, dIdx,
  baM1D, baMND, baP1D, baPND, 
  r, rExp, rSol, rSub, recEqList, qM1D, qP1D],
  
  rL : getRecov4CellGen(dirs, dx, polyOrder, qTL, qTC, qBC, qBL),
  rC : getRecov2CellGen(dirs[2], dx, polyOrder, qBC, qTC),
  rR : getRecov4CellGen(dirs, dx, polyOrder, qTC, qTR, qBR, qBC),
  
  numDims : length(dx),
  dIdx : getDirIdx(dirs[1], numDims),
  load("basis-precalc/basisSer1x"),
  basis1D : subst(x=dirs[1], basisC[polyOrder]),
  baC1D : etaDir(dirs[1], 0, dx[dIdx], basis1D),

  N : polyOrder + 3,
  if C = 1 then N : N + 2,
  /* Forming and solving the equation system */
  rExp : doExpand(r, makelist(dirs[1]^i, i, 0, N-1)),
  recEqList : append(
    [subst([dirs[1]=0, dirs[2]=0], rL) - subst(dirs[1]=-dx[dIdx]/2, rExp)],
    [subst([dirs[1]=0, dirs[2]=0], rR) - subst(dirs[1]=dx[dIdx]/2, rExp)],
    calcInnerProdListGen([dirs[1]], [[-dx[dIdx]/2, dx[dIdx]/2]], 1, baC1D, rExp-subst(dirs[2]=0, rC))
    ),
  if C = 1 then (
    recEqList : append(
      recEqList,
      [subst([dirs[1]=0, dirs[2]=0], diff(rL, dirs[1])) - subst(dirs[1]=-dx[dIdx]/2, diff(rExp, dirs[1]))],
      [subst([dirs[1]=0, dirs[2]=0], diff(rR, dirs[1])) - subst(dirs[1]=dx[dIdx]/2, diff(rExp, dirs[1]))]
      )
    ),
  rSol : linsolve(recEqList, makelist(r[i], i, 1, N)),
  rSub : fullratsimp(subst(rSol, rExp)),
  return(rSub)
  ) $