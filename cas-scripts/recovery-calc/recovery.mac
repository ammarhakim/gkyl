load("modal-basis") $
assume(dv[0]>0, dv[1]>0, dv[2]>0) $

/* A helper function to shift basis functions in only one direction.
Example: etaDir(y, dy/2, dy, basis) */  
etaDir(dir, xc, dx, basis) := subst(td=dir, subst(dir=2*(td-xc)/dx, basis)) $

/* A helper function to shift basis functions in all directions together
Example: eta([x, y], [dx/2, -dy/2], [dx, dy], basis( */
eta(xc, dx, basis) := block([out, tx, ty, tz],
  out : subst(tx=x, subst(x=2*(tx-xc[1])/dx[1], basis)),
  if length(dx) > 1 then ( out : subst(ty=y, subst(y=2*(ty-xc[2])/dx[2], out)) ),
  if length(dx) > 2 then ( out : subst(tz=z, subst(z=2*(tz-xc[3])/dx[3], out)) ),
  return(out)
  ) $

/* A simple helper function to convert x, y, or z to indices 1, 2, or 3 respectively */
getDirIdx(dir, numDims) := block([],
  if numDims = 1 then return(1),
  if dir = x then return(1),
  if dir = y then return(2),
  if dir = z then return(3)
  ) $

/* Returns recovered polynomial across two neigbouring cells in terms of expansion coefficients at the minus side (qLo) and the plus side (qUp).
Inputs:
  dir : direction of the recovery (x, y, or z)
  dx : a list of cell sizes; if the specified length is greater than zero, the no-recovered direction are backsubstitued to the output
  polyOrder : polynomial order of the inputs
  qLo : expansion coeficients of the recovered quatity at the minus side
  qUp : expansion coeficients of the recovered quatity at the plus side
Outputs:
  a polynomial in terms of qLo and qUp
*/
getRecov2CellGen(dir, dx, polyOrder, qLo, qUp) := block(
  [N, basis1D, basisND, numDims, dIdx,
  baLo1D, baLoND, baUp1D, baUpND, 
  r, rExp, rSol, rSub, recEqList, qLo1D, qUp1D],
  
  numDims : length(dx),
  dIdx : getDirIdx(dir, numDims), /* Converting x, y, or z into 1, 2, or 3 */
  load("basis-precalc/basisSer1x"), /* Loading basis functions */
  basis1D : subst(x=dir, basisC[polyOrder]),
  baLo1D : etaDir(dir, -dx[dIdx]/2, dx[dIdx], basis1D),
  baUp1D : etaDir(dir, dx[dIdx]/2, dx[dIdx], basis1D),
  
  /* Forming and solving the equation system */
  N : polyOrder + 1, /* Number of degrees of freedom */
  rExp : doExpand(r, makelist(dir^i, i, 0, 2*N-1)), /* Recovery polynomial */
  recEqList : append( /* Equations frm the weak equality */
    calcInnerProdListGen([dir], [[-dx[dIdx], 0]], 1, baLo1D, rExp-doExpand(qLo1D, baLo1D)),
    calcInnerProdListGen([dir], [[0, dx[dIdx]]], 1, baUp1D, rExp-doExpand(qUp1D, baUp1D))
    ),
  rSol : linsolve(recEqList, makelist(r[i], i, 1, 2*N)),
  rSub : fullratsimp(subst(rSol, rExp)),
  
  /* Backsubstitute the non-recovered directtions if needed */
  if numDims > 1 then (
    load(sconcat("basis-precalc/basisSer", numDims, "x")), /* Loading higher dimensional basis */
    basisND : basisC[polyOrder],
    xc : [0, 0, 0],
    xc[dIdx] : -dx[dIdx]/2,
    baLoND : eta(xc, dx, basisND), /* Shifting the multi-D basis in only one direction */
    xc[dIdx] : dx[dIdx]/2,
    baUpND : eta(xc, dx, basisND), /* Shifting the multi-D basis in only one direction */
    dimProjM : calcInnerProdListGen([dir], [[-dx[dIdx], 0]], 1, baLo1D, doExpand(qLo, baLoND)),
    dimProjP : calcInnerProdListGen([dir], [[0, dx[dIdx]]], 1, baUp1D, doExpand(qUp, baUpND)),
    projSubList : append ( /* Substitution list fot the backprojection */
      makelist(qLo1D[i]=dimProjM[i], i, 1, length(basis1D)),
      makelist(qUp1D[i]=dimProjP[i], i, 1, length(basis1D))
      )
    )
  else (
    projSubList : append (
      makelist(qLo1D[i]=qLo[i], i, 1, length(basis1D)),
      makelist(qUp1D[i]=qUp[i], i, 1, length(basis1D))
      )
    ),

  return(subst(projSubList, rSub))
  ) $

/* Returns recovered polynomial across three cells in terms of expansion coefficients at the minus side (qLo), center (qCe), and the plus side (qUp).
This is done in two stages. In the first stage, getRecov2CellGen is used to calculate recovery polynomials at the two pairs of cells. These are then evaluated at the interfaces (only values for C=0; values and first derivatives for C=1). This addition information is then used to recover higher order solution in the central cell. 
Inputs:
  dir : direction of the recovery (x, y, or z)
  dx : a list of cell sizes; if the specified length is greater than zero, the no-recovered direction are backsubstitued to the output
  polyOrder : polynomial order of the inputs
  C : continuity of the recovered solution (either 0 or 1)
  qLo : expansion coeficients of the recovered quatity at the minus side
  qCe : expansion coeficients of the recovered quatity in the central cell
  qUp : expansion coeficients of the recovered quatity at the plus side
Outputs:
  a polynomial in terms of qLo, qCe, and qUp
*/
getRecov3CellGen(dir, dx, polyOrder, C, qLo, qCe, qUp) := block(
  [N, basis1D, basisND, numDims,
  baLo1D, baLoND, baC1D, baCND, baUp1D, baUpND,
  r, rExp, rSol, rSub, recEqList, qLo1D, qCe1D, qUp1D,
  rM, rP],
  
  numDims : length(dx),
  dIdx : getDirIdx(dir, numDims),
  load("basis-precalc/basisSer1x"),
  basis1D : subst(x=dir, basisC[polyOrder]),
  baC1D : etaDir(dir, 0, dx[dIdx], basis1D),

  N : polyOrder + 3,
  if C = 1 then N : N + 2, /* Increase the final namuber of degrees of freedom for C_1 continuity */
  /* Forming and solving the equation system */
  rExp : doExpand(r, makelist(dir^i, i, 0, N-1)), /* Final recovery polynomial */
  /* Recover solutions across two pairs of cells */
  rM : getRecov2CellGen(dir, [dx[dIdx]], polyOrder, qLo1D, qCe1D),
  rP : getRecov2CellGen(dir, [dx[dIdx]], polyOrder, qCe1D, qUp1D),
  recEqList : append(
    [subst(dir=0, rM) - subst(dir=-dx[dIdx]/2, rExp)], /* Prescribing solution at the minus interface */
    [subst(dir=0, rP) - subst(dir=dx[dIdx]/2, rExp)], /* Prescribing solution at the plus interface */
    calcInnerProdListGen([dir], [[-dx[dIdx]/2, dx[dIdx]/2]], 1, baC1D, rExp-doExpand(qCe1D, baC1D)) /* New solution is weakly equal to the original representation */
    ),
  if C = 1 then (
    recEqList : append(
      recEqList,
      [subst(dir=0, diff(rM, dir)) - subst(dir=-dx[dIdx]/2, diff(rExp, dir))],
      [subst(dir=0, diff(rP, dir)) - subst(dir=dx[dIdx]/2, diff(rExp, dir))]
      )
    ),
  rSol : linsolve(recEqList, makelist(r[i], i, 1, N)),
  rSub : fullratsimp(subst(rSol, rExp)),
  
  /* Backsubstitute the non-recovered directtions if needed */
  if numDims > 1 then (
    baLo1D : etaDir(dir, -dx[dIdx], dx[dIdx], basis1D),
    baUp1D : etaDir(dir, dx[dIdx], dx[dIdx], basis1D),
    load(sconcat("basis-precalc/basisSer", numDims, "x")),
    basisND : basisC[polyOrder],
    xc : [0, 0, 0],
    xc[dIdx] : -dx[dIdx],
    baLoND : eta(xc, dx, basisND),
    xc[dIdx] : 0,
    baCND : eta(xc, dx, basisND),
    xc[dIdx] : dx[dIdx],
    baUpND : eta(xc, dx, basisND),
    
    dimProjM : calcInnerProdListGen([dir], [[-3*dx[dIdx]/2, -dx[dIdx]/2]], 1, baLo1D, doExpand(qLo, baLoND)),
    dimProjC : calcInnerProdListGen([dir], [[-dx[dIdx]/2, dx[dIdx]/2]], 1, baC1D, doExpand(qCe, baCND)),
    dimProjP : calcInnerProdListGen([dir], [[dx[dIdx]/2, 3*dx[dIdx]/2]], 1, baUp1D, doExpand(qUp, baUpND)),
    projSubList : append (
      makelist(qLo1D[i]=dimProjM[i], i, 1, length(basis1D)),
      makelist(qCe1D[i]=dimProjC[i], i, 1, length(basis1D)),
      makelist(qUp1D[i]=dimProjP[i], i, 1, length(basis1D))
      )
    )
  else (
    projSubList : append (
      makelist(qLo1D[i]=qLo[i], i, 1, length(basis1D)),
      makelist(qCe1D[i]=qCe[i], i, 1, length(basis1D)),
      makelist(qUp1D[i]=qUp[i], i, 1, length(basis1D))
      )
    ),

  return(subst(projSubList, rSub))
  ) $


getRecov4CellGen(dirs, dx, polyOrder, qTL, qTR, qBL, qBR) := block(
  [N, basis1D, basisND, numDims, dIdx,
  baLo1D, baLoND, baUp1D, baUpND, 
  r, rExp, rSol, rSub, recEqList, qLo1D, qUp1D],
  
  numDims : length(dx),
  dIdx1 : getDirIdx(dirs[1], numDims),
  dIdx2 : getDirIdx(dirs[2], numDims),
  load("basis-precalc/basisSer2x"),
  basis2D : subst([x=dirs[1], y=dirs[2]], basisC[polyOrder]),
  xc : [0, 0, 0],
  xc[dIdx1] : -dx[dIdx1]/2,
  xc[dIdx2] : dx[dIdx2]/2,
  baTL2D : eta(xc, dx, basis2D),
  xc[dIdx1] : dx[dIdx1]/2,
  xc[dIdx2] : dx[dIdx2]/2,
  baTR2D : eta(xc, dx, basis2D),
  xc[dIdx1] : dx[dIdx1]/2,
  xc[dIdx2] : -dx[dIdx2]/2,
  baBR2D : eta(xc, dx, basis2D),
  xc[dIdx1] : -dx[dIdx1]/2,
  xc[dIdx2] : -dx[dIdx2]/2,
  baBL2D : eta(xc, dx, basis2D),
  
  /* Forming and solving the equation system */
  N : polyOrder + 1,
  rExp : doExpand(r, create_list(dirs[1]^i*dirs[2]^j, i, 0, 2*N-1, j, 0, 2*N-1)),
  recEqList : append(
    calcInnerProdListGen(dirs, [[-dx[dIdx1], 0], [0, dx[dIdx2]]], 1, baTL2D, rExp-doExpand(qTL, baTL2D)),
    calcInnerProdListGen(dirs, [[0, dx[dIdx1]], [0, dx[dIdx2]]], 1, baTR2D, rExp-doExpand(qTR, baTR2D)),
    calcInnerProdListGen(dirs, [[-dx[dIdx1], 0], [-dx[dIdx2], 0]], 1, baBL2D, rExp-doExpand(qBL, baBL2D)),
    calcInnerProdListGen(dirs, [[0, dx[dIdx1]], [-dx[dIdx2], 0]], 1, baBR2D, rExp-doExpand(qBR, baBR2D))
    ),
  rSol : linsolve(recEqList, makelist(r[i], i, 1, 4*N*N)),
  rSub : fullratsimp(subst(rSol, rExp)),

  /* if numDims > 2 then ( */
  /*   /\* Creating substitution lists to get the y-dependence *\/ */
  /*   load(sconcat("basis-precalc/basisSer", numDims, "x")), */
  /*   basisND : basisC[polyOrder], */
  /*   xc : [0, 0, 0], */
  /*   xc[dIdx1] : -dx[dIdx1]/2, */
  /*   xc[dIdx2] : dx[dIdx2]/2, */
  /*   baTLND : eta(xc, dx, basisND), */
  /*   xc[dIdx1] : dx[dIdx1]/2, */
  /*   xc[dIdx2] : dx[dIdx2]/2, */
  /*   baTRND : eta(xc, dx, basisND), */
  /*   xc[dIdx1] : dx[dIdx1]/2, */
  /*   xc[dIdx2] : -dx[dIdx2]/2, */
  /*   baBRND : eta(xc, dx, basisND), */
  /*   xc[dIdx1] : -dx[dIdx1]/2, */
  /*   xc[dIdx2] : -dx[dIdx2]/2, */
  /*   baBLND : eta(xc, dx, basisND), */
    
  /*   dimProjTL : calcInnerProdListGen(dirs, [[-dx[dIdx1], 0], [0, dx[dIdx2]]], 1, baTL2D, doExpand(qTL, baTLND)), */
  /*   dimProjTR : calcInnerProdListGen(dirs, [[0, dx[dIdx1]], [0, dx[dIdx2]]], 1, baTR2D, doExpand(qTR, baTRND)), */
  /*   dimProjBR : calcInnerProdListGen(dirs, [[0, dx[dIdx1]], [-dx[dIdx2], 0]], 1, baBR2D, doExpand(qBR, baBRND)), */
  /*   dimProjTL : calcInnerProdListGen(dirs, [[-dx[dIdx1], 0], [-dx[dIdx2], 0]], 1, baBL2D, doExpand(qBL, baBLND)), */
  /*   projSubList : append ( */
  /*     makelist(qTL2D[i]=dimProjTL[i], i, 1, length(basis2D)), */
  /*     makelist(qTR2D[i]=dimProjTR[i], i, 1, length(basis2D)), */
  /*     makelist(qBR2D[i]=dimProjBR[i], i, 1, length(basis2D)), */
  /*     makelist(qBL2D[i]=dimProjBL[i], i, 1, length(basis2D)) */
  /*     ) */
  /*   ) */
  /* else ( */
  /*   projSubList : append ( */
  /*     makelist(qTL2D[i]=qTL[i], i, 1, length(basis2D)), */
  /*     makelist(qTR2D[i]=qTR[i], i, 1, length(basis2D)), */
  /*     makelist(qBR2D[i]=qBR[i], i, 1, length(basis2D)), */
  /*     makelist(qBL2D[i]=qBL[i], i, 1, length(basis2D)) */
  /*     ) */
  /*   ), */

/*  return(subst(projSubList, rSub))*/
  return(rSub)
  ) $


getRecov6CellGen(dominantDir, dx, polyOrder, C, der, q1, q2, q3, q4, q5, q6) := block(
  [N, basis1D, basisND, numDims, dIdx,
  baLo1D, baLoND, baUp1D, baUpND, 
  r, rExp, rSol, rSub, recEqList, qLo1D, qUp1D],
  
  dirs : [x, y],
  
  if dominantDir = x then (
    rL : getRecov4CellGen(dirs, dx, polyOrder, q1, q2, q4, q5),
    rC : getRecov2CellGen(dirs[2], dx, polyOrder, q5, q2),
    for d : 1 thru der[2] do (
      rC : diff(rC, dirs[2])
      ),
    rR : getRecov4CellGen(dirs, dx, polyOrder, q2, q3, q5, q6),
    
    /* numDims : length(dx), */
    dIdx : getDirIdx(dirs[1], numDims),
    load("basis-precalc/basisSer1x"),
    basis1D : subst(x=dirs[1], basisC[polyOrder]),
    baC1D : etaDir(dirs[1], 0, dx[dIdx], basis1D),
    
    N : polyOrder + 3,
    if C = 1 then N : N + 2,
    /* Forming and solving the equation system */
    rExp : doExpand(r, makelist(dirs[1]^i, i, 0, N-1)),
    recEqList : append(
      [subst([dirs[1]=0, dirs[2]=0], rL) - subst(dirs[1]=-dx[dIdx]/2, rExp)],
      [subst([dirs[1]=0, dirs[2]=0], rR) - subst(dirs[1]=dx[dIdx]/2, rExp)],
      calcInnerProdListGen([dirs[1]], [[-dx[dIdx]/2, dx[dIdx]/2]], 1, baC1D, rExp-subst(dirs[2]=0, rC))
      ),
    if C = 1 then (
      recEqList : append(
        recEqList,
        [subst([dirs[1]=0, dirs[2]=0], diff(rL, dirs[1])) - subst(dirs[1]=-dx[dIdx]/2, diff(rExp, dirs[1]))],
        [subst([dirs[1]=0, dirs[2]=0], diff(rR, dirs[1])) - subst(dirs[1]=dx[dIdx]/2, diff(rExp, dirs[1]))]
        )
      ),
    rSol : linsolve(recEqList, makelist(r[i], i, 1, N)),
    rSub : fullratsimp(subst(rSol, rExp)),
    for d : 1 thru der[1] do (
      rSub : diff(rSub, dirs[1])
      )
    ),
  if dominantDir = y then (
    rB : getRecov4CellGen(dirs, dx, polyOrder, q3, q4, q5, q6),
    rC : getRecov2CellGen(dirs[1], dx, polyOrder, q3, q4),
    for d : 1 thru der[1] do (
      rC : diff(rC, dirs[1])
      ),
    rT : getRecov4CellGen(dirs, dx, polyOrder, q1, q2, q3, q4),
    
    /* numDims : length(dx), */
    dIdx : getDirIdx(dirs[2], numDims),
    load("basis-precalc/basisSer1x"),
    basis1D : subst(x=dirs[2], basisC[polyOrder]),
    baC1D : etaDir(dirs[2], 0, dx[dIdx], basis1D),
    
    N : polyOrder + 3,
    if C = 1 then N : N + 2,
    /* Forming and solving the equation system */
    rExp : doExpand(r, makelist(dirs[2]^i, i, 0, N-1)),
    recEqList : append(
      [subst([dirs[1]=0, dirs[2]=0], rB) - subst(dirs[2]=-dx[dIdx]/2, rExp)],
      [subst([dirs[1]=0, dirs[2]=0], rT) - subst(dirs[2]=dx[dIdx]/2, rExp)],
      calcInnerProdListGen([dirs[2]], [[-dx[dIdx]/2, dx[dIdx]/2]], 1, baC1D, rExp-subst(dirs[1]=0, rC))
      ),
    if C = 1 then (
      recEqList : append(
        recEqList,
        [subst([dirs[1]=0, dirs[2]=0], diff(rB, dirs[2])) - subst(dirs[2]=-dx[dIdx]/2, diff(rExp, dirs[2]))],
        [subst([dirs[1]=0, dirs[2]=0], diff(rT, dirs[2])) - subst(dirs[2]=dx[dIdx]/2, diff(rExp, dirs[2]))]
        )
      ),
    rSol : linsolve(recEqList, makelist(r[i], i, 1, N)),
    rSub : fullratsimp(subst(rSol, rExp)),
    for d : 1 thru der[2] do (
      rSub : diff(rSub, dirs[2])
      )
    ),

  return(rSub)
  ) $
