load("modal-basis")$
load("out-scripts");
load(stringproc)$
fpprec : 24$

/* This script generates the kernels needed for adding the
   contributions from second order diffusion in all directions
   using two integrations by parts. */

cvars : [X, Y, Z]$

cidx(cdim) := makelist(i,i,0,cdim-1)$

writeCExprsCollect1s(lhs, rhs, clst, nSpaces) := block([expr],
  expr : float(expand(rhs)),
  for i : 1 thru length(expr) do (
    if expr[i] # 0.0 then (
      param : cons(expr[i], clst),
      e : apply(collectterms, param),
      ep : string(e),
      ep : ssubst("g1Sq","g1^2",ep),
      ep : ssubst("std::sinh","sinh",ep),
      printf(fh, sconcat(smake(nSpaces," "),"  ~a = ~a; ~%"), lhs[i-1], ep)
    )
  )
)$

writeCIncrExprs1s(lhs, rhs, nSpaces) := block([expr],
  expr : float(expand(rhs)),
  for i : 1 thru length(expr) do (
    ep : string(expr[i]),
    ep : ssubst("g1Sq","g1^2",ep),
    ep : ssubst("std::sinh","sinh",ep),
    if expr[i] # 0.0 then printf(fh, sconcat(smake(nSpaces," "),"  ~a += ~a; ~%"), lhs[i-1], ep)
  )
)$

writeCIncrCollect1s(lhs, rhs, clst, nSpaces) := block([expr],
  expr : float(expand(rhs)),
  for i : 1 thru length(expr) do (
    if expr[i] # 0.0 then (
      param : cons(expr[i], clst),
      e  : gcfac(apply(collectterms, param)),
      ep : string(e),
      ep : ssubst("g1Sq","g1^2",ep),
      ep : ssubst("std::sinh","sinh",ep),
      printf(fh,sconcat(smake(nSpaces," "),"  ~a += ~a; ~%"), lhs[i-1], ep)
    )
  )
)$

writeCExprs1s(lhs, rhs, nSpaces) := block([expr],
  expr : expand(float(rhs)),
  for i : 1 thru length(expr) do (
    if expr[i] # 0.0 then (
      ep : string(expr[i]),
      ep : ssubst("std::sinh","sinh",ep),
      for iC : 0 thru Ncp-1 do (
        ep : ssubst(sconcat("xBarSq[",string(iC),"]"),
                    sconcat("xBar[",string(iC),"]^2"),ep),
        ep : ssubst(sconcat("g1Sq"),
                    sconcat("g1[",string(iC),"]^2"),ep)
      ),
      printf(fh,sconcat(smake(nSpaces," "),"  ~a = ~a; ~%"), lhs[i-1], ep)
    )
  )
)$

writeCExprs1sI(lhs, iOut, rhs, nSpaces) := block([expr],
  expr : expand(float(rhs)),
  for i : 1 thru length(expr) do (
    if expr[i] # 0.0 then (
      ep : string(expr[i]),
      ep : ssubst("std::sinh","sinh",ep),
      for iC : 0 thru Ncp-1 do (
        ep : ssubst(sconcat("xBarSq[",string(iC),"]"),
                    sconcat("xBar[",string(iC),"]^2"),ep),
        ep : ssubst(sconcat("g1Sq"),
                    sconcat("g1[",string(iC),"]^2"),ep)
      ),
      printf(fh,sconcat(smake(nSpaces," "),"  ~a = ~a; ~%"), lhs[iOut], ep)
    )
  )
)$

writeCExprs1so(lhs, rhs, nSpaces) := block([expr],
  for i : 1 thru length(rhs) do (
    expr : float(expand(num(rhs[i]))/denom(rhs[i])),
    if expr # 0 then (
      printf(fh,sconcat(smake(nSpaces," "),"  ~a = ~a; ~%"), lhs[i-1], expr)
    )
  )
)$

writeCExprs1sP(lhs, rhs, nSpaces) := block([expr],
  expr : expand(float(rhs)),
  for i : 1 thru length(expr) do (
    if expr[i] # 0.0 then (
      ep : string(expr[i]),
      for iC : 0 thru Ncp-1 do (
        ep : ssubst(sconcat("xBar[",string(iC),"]*xBar[",string(iC),"]"),
                    sconcat("xBar[",string(iC),"]^2"),ep)
      ),
      printf(fh,sconcat(smake(nSpaces," "),"  ~a = ~a; ~%"), lhs[i-1], ep)
    )
  )
)$

/* Given the value of the recovery polynomial (fRec) and its derivative
   (frecP) at the interface, this function computes the increments and
   writes them to the kernel file. */
compWriteIncr(fRec, fRecP, nSpaces) := block([incr_s1,incr_s2],

    /* Contribution to vector to the left and right of the boundary. */
    /* incr_r2 has 3 minus signs. One comes from the second integration
    ** by parts, another from evaluating at upper surface Minus lower
    ** surface, and the last one from evaluating at cv=-1. */
    incr_l1 : calcInnerProdList(surfVars,1,subst(cv=1,bC),fRecP),
    incr_l2 : calcInnerProdList(surfVars,-2,subst(cv=1,bCp),fRec),

    incr_r1 : -calcInnerProdList(surfVars,1,subst(cv=-1,bC),fRecP),
    incr_r2 : -calcInnerProdList(surfVars,-2,subst(cv=-1,bCp),fRec),
    
    clst : [wxr, dvxr],
    writeCExprsCollect1s(incr1, incr_r1, clst, nSpaces),
    printf(fh,"~%"),
    writeCExprsCollect1s(incr2, incr_r2, clst, nSpaces),
    printf(fh, "~%"),

    incr_s1 : makelist(incr1[i-1], i, 1, N),
    incr_s2 : makelist(incr2[i-1], i, 1, N),
    /* Don't do coefficients where the increment is zero */
    for i : 1 thru N do (
      if (incr_r1[i] = 0) then
        incr_s1[i] : 0,
      if (incr_r2[i] = 0) then
        incr_s2[i] : 0
    ), 
    writeCIncrExprs1s(outr, rdxSq2nur*(incr_s1+incr_s2), nSpaces),
    printf(fh, "~%"),
  
    signs1 : makelist(1,i,1,N),
    signs2 : makelist(1,i,1,N),
    for i : 1 thru N do (
        if (incr_r1[i] = 0) then
          false
        else
          signs1[i] : fullratsimp(incr_l1[i]/incr_r1[i]),
        if (incr_r2[i] = 0) then
          false
        else
          signs2[i] : fullratsimp(incr_l2[i]/incr_r2[i])
    ),
    incr_s1 : makelist(signs1[i]*incr1[i-1], i, 1, N),
    incr_s2 : makelist(signs2[i]*incr2[i-1], i, 1, N),
    /* Don't do coefficients where the increment is zero */
    for i : 1 thru N do (
      if (incr_r1[i] = 0) then
        incr_s1[i] : 0
    ), 
    for i : 1 thru N do (
      if (incr_r2[i] = 0) then
        incr_s2[i] : 0
    ), 
    writeCIncrExprs1s(outl, rdxSq2nul*(incr_s1+incr_s2),nSpaces)
)$

calcUpdateInDir(dir, fh, funcNm, cdim, basisFun, pMax, applyPos) := block([],

  /* Positivity preserving algorithm only for p=1 for now. */
  if applyPos then (
    pOrderMax : 1
  ) else (
    pOrderMax : pMax
  ),

  for polyOrder : 1 thru pOrderMax do (

    kill(varsC, varsP, basisC, basisP),

    /*... Compute  1D recovery polynomial coefficients into hsol ...*/
    /* Load 1D basis. */
    load(sconcat("basis-precalc/basis", basisFun, 1, "x")),
    bC1 : basisC[polyOrder],
    N1 : length(bC1),

    eta(xc,dx,b) := subst(x=(w-xc)/(dx/2), b),

    /* Construct the 1D recovery polynomial */
    hOrder : 2*polyOrder+1,
    /* Left and right (of the boundary) basis sets */ 
    pL : eta(-1/2,1,bC1),
    pR : eta(1/2,1,bC1),
    /* Recovery polynomial expanded in z */
    h1 : doExpand1(hn,makelist(w^i,i,0,hOrder)),
    
    /* LHS of system of equations arising from weak equivalence relations */
    eqL : makelist(integrate(h1*pL[i],w,-1,0), i,1,polyOrder+1),
    eqR : makelist(integrate(h1*pR[i],w,0,1), i,1,polyOrder+1),
    LHS : append(eqL,eqR),
    A : coefmatrix(LHS, makelist(hn[i],i,0,hOrder)),
    
    /* RHS is similar to the mass matrix */
    RHS : transpose(append(makelist(fl[i]/2,i,1,N1), makelist(fr[i]/2,i,1,N1))),
    
    /* solve system of equations for coefficients of h */
    S : fullratsimp(invert(A) . RHS),
    hsol1 : makelist(S[i][1],i,1,hOrder+1),

    /*... Recovery polynomial coefficients of requested dimensionality ...*/
    kill(varsC, basisC),
    /* Load basis of dimensionality requested. */
    modNm : sconcat("basis-precalc/basis", basisFun, cdim, "x"),
    load(modNm),

    /* Variables. */
    cv : varsC[dir],
    surfVars : delete(cv,varsC),

    /* Polynomial basis and its derivatives. */
    bC : basisC[polyOrder],
    N : length(bC),
    bCp : diff(bC,cv),

    fg : doExpand(f, bC),	/* Function expanded in basis. */

    /* Obtain the left and right function coefficients
       by projecting the function onto the direction of interest. */
    hcoeff : calcInnerProdList([cv], 1, subst(x=cv,bC1), fg),
    fls : subst(makelist(f[i]=fl[i],i,N), hcoeff),
    frs : subst(makelist(f[i]=fr[i],i,N), hcoeff),

    /* Substitute these "left" and "right" function coefficients,
       which contain variation along other dimensions, into the
       1D recovery polynomial coefficients computed above. */
    hsol : psubst(append(makelist(fl[i]=fls[i], i, 1, N1), makelist(fr[i]=frs[i], i, 1, N1)), hsol1),
    
    /* Write out the full recovery polynomial. */
    h : sum(hsol[i]*cv^(i-1), i, 1, 2*N1),
    /* Evaluate the recovery polynomial at the discontinuity. */
    hsolp0 : subst(cv=0,diff(h,cv)),

    /*............. RECOVERY DONE ..............................*/

    /* Make references to fl and fr in hsol zero-indexed. */
    hsol : psubst(append(makelist(fr[i]=fr[i-1],i,1,N),makelist(fl[i]=fl[i-1],i,1,N)),hsol),

    printf(fh,"void ~a_~a_P~a(const double *wl, const double *wr, const double *dxvl, const double *dxvr, const double *nu, const double *fl, const double *fr, double *outl, double *outr) ~%{ ~%", funcNm, cvars[dir], polyOrder),
    printf(fh,"  // w[~a]:      Cell-center coordinates.~%",cdim),
    printf(fh,"  // dxv[~a]:    Cell spacing.~%",cdim),
    printf(fh,"  // nu[~a]:     diffusion coefficient (collisionality).~%",cdim),
    printf(fh,"  // fl/fr:     Distribution function in left/right cells.~%"),
    printf(fh,"  // outl/outr: Incremented distribution function in left/right cells ~%"),
    cid : cidx(cdim),
    printf(fh,"  double rdxSq2nul = 2.0*nu[~a]/(dxvl[~a]*dxvl[~a]); ~%", cid[dir], cid[dir], cid[dir]),
    printf(fh,"  double rdxSq2nur = 2.0*nu[~a]/(dxvr[~a]*dxvr[~a]); ~%", cid[dir], cid[dir], cid[dir]),
    printf(fh,"~%"),
  
    printf(fh,"  double incr1[~a]; ~%", length(bC)),
    printf(fh,"  double incr2[~a]; ~%", length(bC)),
    printf(fh,"~%"),

    if (not applyPos) then (

      compWriteIncr(hsol[1], hsol[2], 0)

    ) else (

      /* This function evaluates the coefficients of the recovery polynomial
         (fIn) in variables evalVars at all combinations of (+/- evalPoint)
         points, and places it in the list fEvOut. */
      pointEval(fEvOut,fIn,evalVars,evalPoint,compIn) := block([v1,otherVars,fSub],
        v1        : evalVars[1],
        otherVars : delete(v1,evalVars),
        for iQ : -1 step 2 thru 1 do (
          fSub : subst(v1=iQ*evalPoint,fIn),
          if length(otherVars)>0 then (
            pointEval(fEvOut,fSub,otherVars,evalPoint,compIn)
          ) else (
            fEvOut[compIn][iHe] : fSub,
            iHe : iHe + 1
          )
        )
      ),

      /* Evaluate boundary value of recovery polynomial at key (control) points. */
      Ncp : 2^(cdim-1),  /* Number of control points. */
      hsolKP: [makelist(0,j,1,Ncp)],
      iHe : 1,
      if (length(surfVars)>0) then (
        pointEval(hsolKP,hsol[1],surfVars,1/3,1)
      ) else (
        hsolKP[1][iHe] : hsol[1]
      ),


      /* If value of recovery polynomial at the interface, evaluated at key
         points, is below zero, use its exponential representation. */
      hsolKPifCond : "(",
      for iS : 1 thru length(hsolKP[1]) do (
        hsolKPifCond : sconcat(hsolKPifCond,string(float(expand(hsolKP[1][iS]))),">=0.0)"),
        if iS<length(hsolKP[1]) then (
          hsolKPifCond : sconcat(hsolKPifCond," && (")
        )
      ),
      printf(fh,"  if ( ~a ) {~%",hsolKPifCond),

      compWriteIncr(hsol[1], hsol[2], 2),

      printf(fh,"  } else {~%"),
      printf(fh,"~%"),

      Ncp : 2^(cdim-1),  /* Number of control points. */
      hsolEv : makelist(makelist(0,j,1,Ncp),i,1,4),
      for iC : 1 thru 4 do (
        iHe : 1,
        if (length(surfVars)>0) then (
          pointEval(hsolEv,hsol[iC],surfVars,1/3,iC)
        ) else (
          hsolEv[iC][iHe] : hsol[iC]
        )
      ),

      /* At each point we evaluated the recovery poly, need to reconstruct
         an exponential, for which we need xBar, xBarSq and g1. */
      xBars : makelist(0,i,1,Ncp),
      for iCP : 1 thru Ncp do (
        xBars[iCP] : (hsolEv[2][iCP]+3*hsolEv[4][iCP]/5)/(3*hsolEv[1][iCP]+hsolEv[3][iCP])
      ),

      xBarsSq : makelist(xBar[i]*xBar[i],i,0,Ncp-1),
      g1s     : makelist(xBar[i]*(3-xBarSq[i])/(1-xBarSq[i]),i,0,Ncp-1),

      printf(fh,"    double xBar[~a];~%",Ncp),
      writeCExprs1so(xBar,xBars,2),
      printf(fh,"~%"),

      printf(fh,"    double xBarSq[~a];~%", Ncp),
      writeCExprs1sP(xBarSq,xBarsSq,2),
      printf(fh,"~%"),

      printf(fh,"    double g1[~a];~%",Ncp),
      writeCExprs1s(g1,g1s,2),
      printf(fh,"~%"),

      printf(fh,"    double gBound[~a];~%",Ncp),
      printf(fh,"    double gBoundP[~a];~%",Ncp),
      printf(fh,"~%"),

      for iCP : 1 thru Ncp do (
        /* When the function is very flat we need to compute the exponential
           differently so that 1/sinh(g1) doesn't blow up. */
        printf(fh,"    if (std::abs(g1[~a]) > 1.0e-15) {~%",iCP-1),

        printf(fh,"      double g1Sq = g1[~a]*g1[~a];~%",iCP-1,iCP-1),

        expg0  : [g1[iCP-1]*(hsolEv[1][iCP]+hsolEv[3][iCP]/3)/sinh(g1[iCP-1])],
        gprime : [g1[iCP-1]*expg0[1]],

        writeCExprs1sI(gBound,iCP-1,expg0,4),
        writeCExprs1sI(gBoundP,iCP-1,gprime,4),

        printf(fh,"    } else {~%"),

        expg0  : [hsolEv[1][iCP]+hsolEv[3][iCP]/3],
        gprime : [0.0],

        writeCExprs1sI(gBound,iCP-1,expg0,4),
        writeCExprs1sI(gBoundP,iCP-1,gprime,4),

        printf(fh,"    };~%"),
        printf(fh, "~%")
      ),

      if (cdim=1) then (
        compWriteIncr(gBound[0], gBoundP[0], 2)
      ) else (

        /*........... Create nodal to modal transform matrix .......... */
        /* Surface basis: basis of one lower dimensionality w/o surf variable. */
        bSurf  : sqrt(2)*delete(0,fullratsimp(innerProd(varsC,1,bC,subst(cv=0,bC)))*bC),
        gFsurf : doExpand(gF,bSurf),
        /* Evaluate the g-Function at various combinations of control points. */
        gFsurfCP : [makelist(0,i,1,Ncp)],
        iHe      : 1,
        pointEval(gFsurfCP,gFsurf,surfVars,1/3,1),
        /* Construct the nodal-to-modal matrix. */
        n2m : invert(coefmatrix(gFsurfCP[1],makelist(gF[i],i,1,length(bSurf)))),
        /*........... Built nodal to modal transform matrix .......... */

        modalV  : n2m . makelist(gBound[i-1],i,1,Ncp),
        gRec0   : doExpand(makelist(modalV[i][1],i,1,length(bSurf)),bSurf),

        modalV  : n2m . makelist(gBoundP[i-1],i,1,Ncp),
        gRecP0  : doExpand(makelist(modalV[i][1],i,1,length(bSurf)),bSurf),

        compWriteIncr(gRec0,gRecP0,2)
      ), 

      printf(fh,"  };~%")
    ),
    printf(fh, "~%"),
  
  
    printf(fh, "} ~%")
));

calcBoundaryUpdateInDir(dir, fh, funcNm, cdim, basisFun, pMax, applyPos) := block([],

  /* Positivity preserving algorithm only for p=1 for now. */
  if applyPos then (
    pOrderMax : 1
  ) else (
    pOrderMax : pMax
  ),

  for polyOrder : 1 thru pOrderMax do (

    kill(varsC, varsP, basisC, basisP),

    /* Load basis of dimensionality requested. */
    modNm : sconcat("basis-precalc/basis", basisFun, cdim, "x"),
    load(modNm),

    /* Variables. */
    cv : varsC[dir],
    surfVars : delete(cv,varsC),

    /* Polynomial basis and its derivatives. */
    bC  : basisC[polyOrder],
    N   : length(bC),
    bCp : diff(bC,cv),

    /* Function expanded in basis. */
    fl_e : doExpand1(fl, bC),
    fr_e : doExpand1(fr, bC),

    printf(fh,"void ~a_~a_P~a(const double *wl, const double *wr, const double *dxvl, const double *dxvr, const int *idxl, const int *idxr, const double *nu, const double *fl, const double *fr, double *outl, double *outr) ~%{ ~%", funcNm, cvars[dir], polyOrder),
    printf(fh,"  // w[~a]:      Cell-center coordinates.~%",cdim),
    printf(fh,"  // dxv[~a]:    Cell spacing.~%",cdim),
    printf(fh,"  // idx[~a]:    current grid index.~%",cdim),
    printf(fh,"  // nu[~a]:     diffusion coefficient (collisionality).~%",cdim),
    printf(fh,"  // fl/fr:     Distribution function in left/right cells.~%"),
    printf(fh,"  // outl/outr: Incremented distribution function in left/right cells ~%"),
    cid : cidx(cdim),
    printf(fh,"  double rdxSq2nul = 2.0*nu[~a]/(dxvl[~a]*dxvl[~a]); ~%", cid[dir], cid[dir], cid[dir]),
    printf(fh,"  double rdxSq2nur = 2.0*nu[~a]/(dxvr[~a]*dxvr[~a]); ~%", cid[dir], cid[dir], cid[dir]),
    printf(fh,"~%"),
  
    /* Contribution to vector to the left and right of the boundary. */
    /* incr_r2 has 3 minus signs. One comes from the second integration
       by parts, another from evaluating at upper surface Minus lower
       surface, and the last one from evaluating at cv=-1. */
    incr_l2 : calcInnerProdList(surfVars,-2,subst(cv=1,bCp),subst(cv=1,fl_e)),

    incr_r2 : -calcInnerProdList(surfVars,-2,subst(cv=-1,bCp),subst(cv=-1,fr_e)),
    
    clst : [wxr, dvxr],

    printf(fh, "  if (idxr[~a] == 1) {~%", dir),
    printf(fh, "~%"),

    /* Write out contribution right cell. */
    writeCIncrCollect1s(outr, rdxSq2nur*incr_r2, clst, 2),
    printf(fh, "~%"),

    printf(fh, "  } else {~%"),
    printf(fh, "~%"),

    /* Write out contribution to left cell. */
    writeCIncrCollect1s(outl, rdxSq2nul*incr_l2, clst, 2),
    printf(fh, "~%"),

    printf(fh, "  }~%"),

    printf(fh, "~%"),
  
  
    printf(fh, "} ~%")
));

calcConstDiffusionSurfUpdater(fh, funcNm, cdim, basisFun, polyOrderMax, usePos) := block([],
  printf(fh, "#include <ConstDiffusionModDecl.h> ~%"),
  for dir : 1 thru cdim do ( calcUpdateInDir(dir, fh, funcNm, cdim, basisFun, polyOrderMax, usePos) )
)$

calcConstDiffusionBoundarySurfUpdater(fh, funcNm, cdim, basisFun, polyOrderMax, usePos) := block([],
  printf(fh, "#include <ConstDiffusionModDecl.h> ~%"),
  for dir : 1 thru cdim do ( calcBoundaryUpdateInDir(dir, fh, funcNm, cdim, basisFun, polyOrderMax, usePos) )
)$

