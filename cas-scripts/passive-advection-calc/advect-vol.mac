load("modal-basis")$
load("out-scripts")$
load(stringproc)$
load("scifac")$
load("nodalOperations/nodalFunctions")$
fpprec : 24$

wc : [w1, w2, w3, w4, w5, w6]$
derivfac : [dfac1, dfac2, dfac3, dfac4, dfac5, dfac6]$

doMakeExprLst(vals, S) := makelist(if vals[i] # 0 then S[i-1] else 0, i, 1, length(vals))$
doExpandLst(lst, basis) := sum(lst[i]*basis[i], i, 1, length(basis))$

/* set up some vector calculus functions */
load("vect")$

calcAdvectionVolUpdater(fh, funcNm, ndim, basisFun, polyOrder) := block([],
kill(varsC, varsP, basisC, basisP),
  if ndim<=3 then (
    modNm : sconcat("basis-precalc/basis", basisFun, ndim, "x"),
    load(modNm),
    bP : basisC[polyOrder],
    varsP : varsC
  ) else (
    cdim : 3,
    vdim : ndim - cdim,
    modNm : sconcat("basis-precalc/basis", basisFun, cdim, "x", vdim, "v"),
    load(modNm),
    bP : basisP[polyOrder]
  ),
  
  numC : length(bP),

  printf(fh, "double ~aP~a(const double *w, const double *dxv, const double *f, double *out) ~%{ ~%", funcNm, polyOrder),

  printf(fh, "// w[NDIM]: Cell-center coordinates. dxv[NDIM]: Cell spacing.~%"),
  for dir : 1 thru ndim do (
    printf(fh, "  double dfac~a = 2.0/dxv[~a]; ~%", dir, dir-1),
    printf(fh, "  double w~a = w[~a]; ~%", dir, dir-1)
  ),

  for dir : 1 thru ndim do (
    printf(fh, "  const double *v~a = &f[~a]; ~%", dir, numC*(dir))
  ),
  flush_output(fh),

  /* expand distribution function on basis */
  f_expd:doExpand1(f,bP),
  v_expd:[],
  for dir : 1 thru ndim do (
    vd : verbify(sconcat("v",dir)),
    v_expd:append(v_expd, [doExpand1(vd,bP)])
  ),

  /* calculate cflFeq = sum_d alpha_d / dxv[d] */
  printf(fh, "  double cflRate = 0.0; ~%"), 
  printf(fh, "  double alphaL = 0.0; ~%"), 
  printf(fh, "  double alphaR = 0.0; ~%"), 
  
  /* note: no contribution from mu */
  alphaSum_expd : 0,
  for d : 1 thru ndim do (
    alpha_expd : v_expd[d]*derivfac[d],

    /* note: factor of derivfacsD already included in alpha_expd */
    alphaDotGradBasis_expd : alpha_expd*diff(bP, varsP[d]),

    /* accumulate alpha.grad(w) in this direction to the direction-summed alpha.grad(w) */
    alphaSum_expd : alphaSum_expd + alphaDotGradBasis_expd,

    /* cfl calculation */
    /* get quadrature nodes on surface */
    surfIntVars : delete(varsP[d], varsP),
    surfNodes : getNodes(basisFun, ndim-1, polyOrder),
    if polyOrder=1 then (surfNodes:surfNodes*(1/sqrt(3))) elseif polyOrder=2 then (surfNodes:surfNodes*sqrt(3/5)),

    /* evaluate alpha at left and right edge */
    alphaL : subst(varsP[d]=-1, alpha_expd),
    alphaR : subst(varsP[d]=1, alpha_expd),

    /* evaluate cfl by surface averaging */
    printf(fh, "#if cflType == SURFAVG ~%"),
    /* note: factor of derivfacsD already included in alpha_expd */
    alphaL0 : fullratsimp(innerProd(delete(varsP[d],varsP), 1, alphaL, subst(varsP[d]=-1, bP[1]))*bP[1]),
    printf(fh, "  // evaluate surface-averaged alpha on left ~%"),
    printf(fh, "  alphaL = ~a; ~%", gcfac(float(alphaL0))),
    printf(fh, "  cflRate += -0.5*(alphaL-std::abs(alphaL)); ~%"),
    /* note: factor of derivfacsD already included in alpha_expd */
    alphaR0 : fullratsimp(innerProd(delete(varsP[d],varsP), 1, alphaR, subst(varsP[d]=1, bP[1]))*bP[1]),
    printf(fh, "  // evaluate surface-averaged alpha on right ~%"),
    printf(fh, "  alphaR = ~a; ~%", gcfac(float(alphaR0))),
    printf(fh, "  cflRate += 0.5*(alphaR+std::abs(alphaR)); ~%"),
    flush_output(fh),

    /* evaluate cfl by evaluating at all surface quadrature points */
    printf(fh, "#elif cflType == QUAD ~%"),
    alphaLquad : evAtNodes(alphaL*subst(varsP[d]=-1,bP[1]), surfNodes, surfIntVars)*bP[1],
    alphaRquad : evAtNodes(alphaR*subst(varsP[d]=1,bP[1]), surfNodes, surfIntVars)*bP[1],
    printf(fh, "  // evaluate alpha at left surface quadrature points ~%"),
    for i:1 thru length(alphaLquad) do (
      printf(fh, "  alphaL = ~a; ~%", gcfac(float(alphaLquad[i]))),
      printf(fh, "  cflRate += -0.5*(alphaL-std::abs(alphaL)); ~%")
    ),
    printf(fh, "  // evaluate alpha at right surface quadrature points ~%"),
    for i:1 thru length(alphaRquad) do (
      printf(fh, "  alphaR = ~a; ~%", gcfac(float(alphaRquad[i]))),
      printf(fh, "  cflRate += 0.5*(alphaR+std::abs(alphaR)); ~%")
    ),
    printf(fh, "#endif ~%"),

    printf(fh, "~%"),
    flush_output(fh)
    
  ),

  advectVolTerm_k : fullratsimp(innerProd(varsP, 1, f_expd, alphaSum_expd)),
  writeCIncrExprsNoExpand(gcfac(float(advectVolTerm_k))),
  flush_output(fh),

  printf(fh, "  return cflRate; ~%"),
  printf(fh, "} ~%"),
  flush_output(fh)
)$

pmax:1$

for c : 1 thru 5 do (
for p : 1 thru pmax do (
fname : sconcat("~/max-out/PassiveAdvectionSer", c, "xP", p, ".cpp"),
fh : openw(fname),
printf(fh, "#include <PassiveAdvectionModDecl.h> ~%"),
funcName : sconcat("PassiveAdvectionVol", c, "xSer"),
calcAdvectionVolUpdater(fh, funcName, c, "Ser", p),
close(fh)
))$
