/* Compute the volume term for ONLY the streaming (v . grad(f)) term
   in the Vlasov-Poisson and Vlasov-Maxwell equations. */

load("modal-basis");
load("out-scripts");
load(stringproc)$
fpprec : 24$

varsV : [vx, vy, vz]$

cidx(cdim)      := makelist(i,i,0,cdim-1)$
vidx(cdim,vdim) := makelist(i,i,cdim,cdim+vdim-1)$

calcVlasovStreamVolUpdater(fh, funcNm, cdim, vdim, basisFun, polyOrder) := block([bP,cid,vid,wDdx,dvDdx,dir,volTerm,clst,strOut],

  kill(varsC, varsP, basisC, basisP),

  load(sconcat("basis-precalc/basis", basisFun, cdim, "x", vdim, "v")),

  printf(fh, "#include <VlasovModDecl.h> ~%"),

  bP : basisP[polyOrder],

  printf(fh, "__host__ __device__ double ~aP~a(const double *w, const double *dxv, const double *f, double *out) ~%{ ~%", funcNm, polyOrder),
  printf(fh, "  // w[NDIM]: Cell-center coordinates.~%"),
  printf(fh, "  // dxv[NDIM]: Cell spacing.~%"),
  printf(fh, "  // f: Input distribution function.~%"),
  printf(fh, "  // out: Incremented output.~%"),

  cid : cidx(cdim),
  vid : vidx(cdim,vdim),

  wDdx  : makelist(eval_string(sconcat("w",vid[i],"Ddx",cid[i])),i,1,cdim),
  dvDdx : makelist(eval_string(sconcat("dv",vid[i],"Ddx",cid[i])),i,1,cdim),

  /* Cell center and cell length divided by velocity cell length. */
  for dir : 1 thru cdim do (
    printf(fh, "  double w~aDdx~a  = w[~a]/dxv[~a]; ~%", vid[dir], dir-1, vid[dir], cid[dir]),
    printf(fh, "  double dv~aDdx~a = dxv[~a]/dxv[~a]; ~%", vid[dir], dir-1, vid[dir], cid[dir])
  ),

  /* Expand distribution function in phase basis. */
  f_e : doExpand1(f, bP),

  /* Inner product of grad(psi) . v f, where psi is a basis function. */
  volTerm : 0,
  for dir : 1 thru cdim do (
    volTerm : volTerm
             +calcInnerProdList(varsP, 2*wDdx[dir]+dvDdx[dir]*varsV[dir], diff(bP,varsP[dir]), f_e)
  ),
  printf(fh, "~%"),

  clst : append(wDdx,dvDdx),
  writeCIncrExprsCollect1(out,volTerm,clst),
  printf(fh, "~%"),

  strOut : sconcat("std::abs(w",vid[1],"Ddx",0,")+0.5*dv",vid[1],"Ddx",0),
  for d : 2 thru cdim do (
    strOut : sconcat(strOut,"+std::abs(w",vid[d],"Ddx",d-1,")+0.5*dv",vid[d],"Ddx",d-1)
  ),
  printf(fh, "  return ~a;~%",strOut),
  printf(fh, "} ~%")
)$
