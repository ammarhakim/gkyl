load("modal-basis");
load("out-scripts");
load(stringproc)$
fpprec : 24$

varsV : [vx, vy, vz]$
cvars : [X, Y, Z]$
vvars : [VX, VY, VZ]$

doExpand(f, basis) := sum(f[i-1]*basis[i], i, 1, length(basis))$

cidx(cdim) := makelist(i,i,0,cdim-1)$
vidx(cdim,vdim) := makelist(i,i,cdim,cdim+vdim-1)$

calcUpdateInDir(dir, fh, funcNm, cdim, vdim, basisFun) := block([],
kill(varsC, varsP, basisC, basisP),
modNm : sconcat("basis-precalc/basis", basisFun, cdim, "x", vdim, "v"),
load(modNm),
for i : 1 thru 2 do (
  bP : basisP[i],
  printf(fh, "void ~a_~a_P~a(const double *w, const double *dxv, const double *fl, const double *fr, double *outl, double *outr) ~%{ ~%", funcNm, cvars[dir], i),
  printf(fh, "// w[NDIM]: Cell-center coordinates. dxv[NDIM]: Cell spacing. fl/fr: Distribution function in left/right cells ~%"),
  printf(fh, "// outl/outr: Incremented distribution function in left/right cells ~%"),
  cid : cidx(cdim),
  vid : vidx(cdim,vdim),
  printf(fh, "  double dvx = dxv[~a]/dxv[~a]; ~%", vid[dir], cid[dir]),
  printf(fh, "  double wx = w[~a]*2/dxv[~a]; ~%~%", vid[dir], cid[dir]),
  printf(fh, "  double incr[~a]; ~%~%", length(bP)),
  cv : varsP[dir],
  vv : varsV[dir],
  surfVars : delete(cv,varsP),
  fl_L : doExpand(fl, bP),
  fl_R : doExpand(fr, bP),
  printf(fh, "  if (wx>0) { ~%"),
  fhat : subst(cv=1, fl_L),
  t : calcInnerProdList(surfVars, wx+dvx*vv, subst(cv=-1, bP), fhat),
  incr_r : t,
  writeCExprs1(incr, incr_r),
  printf(fh, "~%"),
  incr_s : makelist(incr[i-1], i, 1, length(bP)),
  writeCIncrExprs1(outr, incr_s),  
  printf(fh, "~%"),
  t : calcInnerProdList(surfVars, wx+dvx*vv, subst(cv=1, bP), fhat),
  incr_l : -t,
  signs : fullratsimp(incr_l/incr_r),
  incr_s : makelist(signs[i]*incr[i-1], i, 1, length(bP)),
  writeCIncrExprs1(outl, incr_s),
  printf(fh, "  } else { ~%"),
  fhat : subst(cv=-1, fl_R),
  t : calcInnerProdList(surfVars, wx+dvx*vv, subst(cv=-1, bP), fhat),
  incr_r : t,
  writeCExprs1(incr, incr_r),
  printf(fh, "~%"),
  incr_s : makelist(incr[i-1], i, 1, length(bP)),
  writeCIncrExprs1(outr, incr_s),
  printf(fh, "~%"),
  t : calcInnerProdList(surfVars, wx+dvx*vv, subst(cv=1, bP), fhat),
  incr_l : -t,
  signs : fullratsimp(incr_l/incr_r),
  incr_s : makelist(signs[i]*incr[i-1], i, 1, length(bP)),
  writeCIncrExprs1(outl, incr_s),
  printf(fh, "  } ~%"),
  printf(fh, "} ~%")
));

calcVlasovSurfUpdater(fh, funcNm, cdim, vdim, basisFun) := block([],
  printf(fh, "#include <VlasovModDecl.h> ~%"),
  for dir : 1 thru cdim do ( calcUpdateInDir(dir, fh, funcNm, cdim, vdim, basisFun) )
)$

