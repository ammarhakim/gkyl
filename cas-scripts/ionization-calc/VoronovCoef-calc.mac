/* Generate kernels for the updater that computes the Voronov
   ionization reaction rate coef <\sigma v_e> */
/* Obtaining a full expansion for the reaction rate is likely
   to be a complex operation, so we will probably need a
   dedicated updater. For now only use cell averages. */

load("modal-basis");
load("out-scripts");
load(stringproc)$
load("scifac")$
fpprec : 24$

/* Serendipity basis. */
maxPolyOrder_Ser : 3$
minCdim_Ser : 1$
maxCdim_Ser : 3$

/* ...... END OF USER INPUTS........ */


writeCExprsCollect1r(lhs, rhs, clst) := block([expr],
  expr : float(expand(rhs)),
  for i : 1 thru length(expr) do (
    if expr[i] # 0.0 then (
      param : cons(expr[i], clst),
      e : apply(collectterms, param),
      ep : string(e),
      outStr : ssubst("pow(U,K)","U^K",ep),
      outStr : ssubst("pow(2,3/2)","2^(3/2)",outStr),
      outStr : ssubst("pow(U,K+1/2)","U^(K+1/2)",outStr),
      outStr : ssubst("exp(U)","e^U",outStr),      
      printf(fh, "    ~a = ~a; ~%", lhs[i-1], outStr)
    )
  )
)$

declareWriteCExprsC(lhs, rhs) := block([expr],
  expr : float(expand(rhs)),
  if expr # 0.0 then (
    ep : string(expr),
    outStr : ssubst("pow(U,K)","U^K",ep),
    outStr : ssubst("pow(2,3/2)","2^(3/2)",outStr),
    outStr : ssubst("pow(U,K+1/2)","U^(K+1/2)",outStr),
    outStr : ssubst("exp(U)","e^U",outStr), 
    printf(fh, "  double ~a = ~a; ~%", lhs, outStr)
  )
)$

writeCExprsC(lhs, rhs) := block([expr],
  expr : float(expand(rhs)),
  if expr # 0.0 then (
    ep : string(expr),
    outStr : ssubst("pow(U,K)","U^K",ep),
    outStr : ssubst("pow(2,3/2)","2^(3/2)",outStr),
    outStr : ssubst("pow(U,K+1/2)","U^(K+1/2)",outStr),
    outStr : ssubst("exp(U)","e^U",outStr), 
    printf(fh, "  ~a = ~a; ~%", lhs, outStr)
  )
)$

writeCExprsCs(lhs, rhs) := block([expr],
  expr : float(expand(rhs)),
  for i : 1 thru length(expr) do (	
    if expr[i] # 0.0 then (
      ep : string(expr[i]),
      outStr : ssubst("pow(U,K)","U^K",ep),
      outStr : ssubst("pow(2,3/2)","2^(3/2)",outStr),
      outStr : ssubst("pow(U,K+1/2)","U^(K+1/2)",outStr),
      outStr : ssubst("exp(U)","e^U",outStr), 
      printf(fh, "    ~a = ~a; ~%", lhs[i-1], outStr)	
    )
  )
)$

writeCExprsCnos(lhs, rhs) := block([expr],
  expr : float(expand(rhs)),
  if expr # 0.0 then (
    ep : string(expr),
    outStr : ssubst("pow(U,K)","U^K",ep),
    outStr : ssubst("pow(2,3/2)","2^(3/2)",outStr),
    outStr : ssubst("pow(U,K+1/2)","U^(K+1/2)",outStr),
    outStr : ssubst("exp(U)","e^U",outStr), 
    printf(fh, "  ~a = ~a; ~%", lhs, outStr)
  )
)$

calcVoronovReactRateCellAv(fh, funcNm, cdim, basisFun, pMax) := block([],

  kill(varsC, varsP, basisC, basisP),

  load(sconcat("basis-precalc/basis", basisFun, cdim, "x")),

  for polyOrder : 1 thru pMax do (
    bC  : basisC[polyOrder],
    NC  : length(bC),
  
    /* Function declaration with input/output variables. */
    printf(fh, "double ~a_P~a(const double elemCharge, const double m_, const double *m0, const double *vtSqNeut, double vtSqNeutMin, const double *vtSqElc, double vtSqElcMin, const double E, const double A, const double K, const double P, const double X, double *coefIz) ~%{ ~%", funcNm, polyOrder),
    printf(fh, "  // elemCharge: elementary charge (J - eV conversion factor). ~%"),    
    printf(fh, "  // E:   	     Voronov ionization energy. ~%"),
    printf(fh, "  // A:   	     Voronov constant. ~%"),
    printf(fh, "  // K:   	     Voronov constant. ~%"),
    printf(fh, "  // P:   	     Voronov constant. ~%"),
    printf(fh, "  // X:   	     Voronov constant. ~%"),
    printf(fh, "  // m_:             mass of electron. ~%"),
    printf(fh, "  // m0[~a]:         neutral density. ~%", NC),
    printf(fh, "  // vtSqElc[~a]:    electron squared thermal speed, sqrt(T/m). ~%", NC),
    printf(fh, "  // vtSqNeut[~a]:   neutral squared thermal speed, sqrt(T/m). ~%", NC),
    printf(fh, "  // coefIz[~a]:     ionization reaction rate. ~%", NC),
    printf(fh, " ~%"),
    

    m0_e       : doExpand1(m0,bC),
    vtSqNeut_e : doExpand1(vtSqNeut,bC),
    vtSqElc_e  : doExpand1(vtSqElc,bC),
    m0Av       : innerProd(varsC,1.0,1.0,m0_e)/innerProd(varsC,1.0,1.0,1.0),
    vtSqNeutAv : innerProd(varsC,1.0,1.0,vtSqNeut_e)/innerProd(varsC,1.0,1.0,1.0),
    vtSqElcAv  : innerProd(varsC,1.0,1.0,vtSqElc_e)/innerProd(varsC,1.0,1.0,1.0),

    declareWriteCExprsC(m0NeutAv, m0Av),
    declareWriteCExprsC(vtSqElc0, vtSqElcAv),
    declareWriteCExprsC(vtSqNeut0, vtSqNeutAv),

    /* Limit how low the temperature can get (set by the velocity grid). */
    printf(fh, "  if ((vtSqNeut0 > 0.) && (vtSqNeut0 < vtSqNeutMin)) vtSqNeut0 = vtSqNeutMin;~%"),
    printf(fh, "  if ((vtSqElc0 > 0.) && (vtSqElc0 < vtSqElcMin)) vtSqElc0 = vtSqElcMin;~%"),
    printf(fh, " ~%"),

    declareWriteCExprsC(T0, vtSqElc0*m_/elemCharge),

    Uav : E/T0,
    declareWriteCExprsC(U, Uav),
    printf(fh, " ~%"),

    radexpand : false,
    coefIz_c  : calcInnerProdList(varsC,1.0,bC, (A*(1 + P*sqrt(U))/(X + U)*U^K*e^(-U)*1e-6)),

    printf(fh, "  if (U >= 3.0/2.0 || m0NeutAv <= 0. || vtSqNeut0 <= 0. || vtSqElc0 <= 0.) { ~%"),
    printf(fh, "    coefIz[0] = 0.0;~%"),
    printf(fh, "    return 0.0; ~%"),
    printf(fh, "  } else {~%"),

    writeCExprsCs(coefIz, coefIz_c),

    coef_e  : doExpand1(coefIz,bC),
    coefAv  : innerProd(varsC,1.0,1.0,coef_e)/innerProd(varsC,1.0,1.0,1.0),
    cflRate : float(expand(fullratsimp(m0Av*coefAv/(2*polyOrder+1)))),
    printf(fh, "    return ~a; ~%",cflRate),

    printf(fh, "  };~%"),
    printf(fh, "} ~%"),
    printf(fh, " ~%")
  )
)$

bName        : ["Ser"]$
maxPolyOrder : [maxPolyOrder_Ser]$
minCdim      : [minCdim_Ser]$
minVdim      : [minVdim_Ser]$
maxCdim      : [maxCdim_Ser]$
maxVdim      : [maxVdim_Ser]$

for bInd : 1 thru length(bName) do (
  for c : minCdim[bInd] thru maxCdim[bInd] do (
    disp(printf(false,sconcat("Creating Voronov",bName[bInd]," ~ax"),c)),
    fname : sconcat("~/max-out/Voronov", bName[bInd], c, "x", ".cpp"),
    fh : openw(fname),

    printf(fh, "#include <IonizationModDecl.h> ~%"),
    printf(fh, "#include <math.h> ~%"),

    /*. Build collisionality from scratch. */
    funcName : sconcat("VoronovReactRateCellAv", c, "x", bName[bInd]),
    calcVoronovReactRateCellAv(fh, funcName, c, bName[bInd], maxPolyOrder[bInd]),
    close(fh)
  )
)$

